Alphabet
 ᤀ ᤒ ᤍ ᤈ ᤃ ᤛ ᤘ ᤖ ᤕ  ᤠ  ᤧ  ᤣ  ᤡ ᤏ ᤅ ᤋ ᤁ ᤐ ᤗ ᤔ  ᤢ  ᤥ  ᤤ  ᤦ ᤊ ᤂ ᤑ ᤆ ᤇ ᤙ ᤜ ᤌ ᤓ ᤎ ᤉ ᤄ ᤚ ᤫ  ᤪ  ᤩ  ᤨ  ᤴ  ᤱ  ᤳ  ᤰ  ᤵ  ᤸ  ᤶ  ᤹ ᤝ ᤞ  ᤺  ᤷ   ᤻
 %{A%}:ᤠ
 %{Ao%}:ᤠ %{Ao%}:0
 %{gs%}:᤹ %{gs%}:0
 %{a%}:ᤀ
 %{aa%}:ᤀ %{aa%}:0
 %{ana%}:ᤀ %{ana%}:0
 %{o%}:ᤣ %{o%}:ᤥ
 %{oo%}:ᤥ
 %{ao%}:ᤥ %{ao%}:ᤠ
 %{n%}:ᤱ
 %{N%}:ᤏ %{N%}:ᤴ
 %{M%}:ᤍ %{M%}:ᤛ
 %{NG%}:ᤅ %{NG%}:ᤔ %{NG%}:ᤏ
 %{ng%}:ᤅ %{ng%}:ᤱ
 %{k%}:ᤁ %{k%}:ᤰ
 %{K%}:ᤁ %{K%}:ᤃ %{K%}:ᤰ
 %{p%}:ᤐ %{p%}:ᤵ
 %{t%}:ᤋ %{t%}:ᤳ
 %{T%}:ᤋ %{T%}:ᤍ
 %{tp%}:ᤳ %{tp%}:ᤵ
 %{tm%}:ᤔ %{tm%}:ᤋ %{tm%}:0
 %{B%}:ᤒ %{B%}:ᤐ
 %{e%}:ᤣ
 %{do%}:᤺ %{do%}:0
 %{km%}:᤻
 %{m%}:ᤔ %{m%}:ᤶ
 %{L%}:ᤗ
 %{Ee%}:ᤧ
 %{E%}:ᤧ %{E%}:0
 %{l%}:ᤸ
 %{ll%}:ᤸ %{ll%}:ᤗ
 %{i%}:ᤡ
 %{I%}:ᤡ
 %{s%}:ᤛ %{s%}:0 %{s%}:ᤋ
 %{S%}:ᤛ
 %{So%}:ᤛ %{So%}:0
 %{P%}:ᤶ %{P%}:ᤵ
 %{D%}:ᤔ %{D%}:ᤍ
 %{u%}:ᤢ
 %{ch%}:ᤈ %{ch%}:ᤆ
 %{eg%}:᤹ %{eg%}:ᤣ
 %{NGK%}:ᤱ %{NGK%}:ᤰ
 %{pgt%}:᤹ %{pgt%}:ᤵ %{pgt%}:ᤳ
 %{NM%}:ᤶ %{NM%}:ᤴ
 %{Dnull%}:ᤍ %{Dnull%}:0
 %{dg%}:᤺ %{dg%}:᤹
 %{R%}:ᤖ %{R%}:0 %{R%}:ᤶ
 %{BH%}:ᤑ %{BH%}:ᤓ
 %>:0  ;

Sets

Vowel = ᤠ  ᤧ  ᤣ  ᤡ   ᤢ  ᤥ  ᤤ  ᤦ  ᤺   ᤻  ᤨ ;
Bilabial = ᤔ ᤐ ᤑ ᤒ ᤓ  ᤵ  ᤶ ;
iorE = ᤧ  ᤡ ;
Vowelnota = ᤧ  ᤣ  ᤡ   ᤢ  ᤥ  ᤤ  ᤦ  ᤺   ᤻  ᤨ ;
Vowelnote = ᤠ  ᤣ  ᤡ   ᤢ  ᤥ  ᤤ  ᤦ  ᤺   ᤻  ᤨ ;
Vowelnotioru = ᤠ  ᤧ  ᤣ  ᤥ  ᤤ  ᤦ  ᤺   ᤻  ᤨ ;
Consonant = ᤒ ᤍ ᤈ ᤃ ᤛ ᤘ ᤖ ᤕ ᤏ ᤅ ᤋ ᤁ ᤐ ᤗ ᤔ ᤊ ᤂ ᤑ ᤆ ᤇ ᤙ ᤜ ᤌ ᤓ ᤎ ᤉ ᤄ ᤚ ᤫ  ᤪ  ᤩ  ᤴ  ᤱ  ᤳ  ᤰ  ᤵ  ᤸ  ᤶ ᤝ ᤞ ;

Rules
"ᤅᤠᤱ becomes ᤏᤠᤱ after a dental stop"
%{NG%}:ᤏ <=> :[ᤋ | ᤍ] %>: _ ;
"ᤅᤠᤱ becomes ᤔᤠᤱ after a bilabial"
%{NG%}:ᤔ <=> :Bilabial %>: _ ;
"{aa} becomes ᤀ after a vowel but not a"
%{aa%}:ᤀ <=> :Vowelnota %>: _ ;
"{aa} becomes null after a"
%{aa%}:0 <=> :ᤠ %>: _ ;
"{aa} becomes null after a consonant"
%{aa%}:0 <=> :Consonant %>: _ ;
"{ana} becomes ᤀ after a Vowel"
%{ana%}:ᤀ <=> :Vowel %>: _ ;
"{ana} becomes null after a consonant"
%{ana%}:0 <=> :Consonant %>: _ ;
"{i} becomes null if succeeded by i"
%{i%}:0 <=> _ %>: :ᤡ ;
            :Vowel %>: _ ;
"{I} becomes null if succeeded by i"
%{I%}:0 <=> _ %>: :ᤡ ;
"{A} becomes null if succeeded by a vowel"
%{A%}:0 <=> _ %>: ( :0 ) :Vowel ;
            :ᤠ %>: ( :0 ) _ ;
"{o} becomes o if preceded by vowel that isnt u or i"
%{o%}:ᤥ <=> :Vowelnotioru %>: :ᤀ _ ;
"{E} becomes null if preceded by i"
%{E%}:0 <=> :ᤡ %>: _ ;

"{s} becomes null if at the end"
%{s%}:0 <=> _ ( :0 )  .#. ;
"{s} becomes s if followed by e"
%{s%}:ᤛ <=> _ ( :0 )  :ᤧ  ;
"{s} becomes t if followed by vowel not e"
%{s%}:ᤋ <=> _ ( :0 )  :Vowelnote ;
"{P} becomes p if followed by s"
%{P%}:ᤵ <=> _ :ᤛ ;
"{D} becomes d if followed by E"
%{D%}:ᤍ <=> _ ( :0 ) ( %>: ) :ᤧ  ;
"{R} becomes r if followed by vowel?"
%{R%}:ᤖ <=> _ ( :0 ) %>: ( :0 ) :Vowel ;
"{R} becomes null if at the end"
%{R%}:0 <=> _ ( :0 )  .#. ;

"{m} becomes ᤔ if succeeded by a vowel"
%{m%}:ᤔ <=> _ ( %>: ) ( :0 ) :Vowel ;
"{N} becomes ᤏ if succeeded by a vowel"
%{N%}:ᤏ <=> _ ( %>: ) ( :0 ) :Vowel ;
"{ng} becomes ᤅ if succeeded by a vowel"
%{ng%}:ᤅ <=> _ ( %>: ) ( :0 ) :Vowel ;
"{k} becomes ᤁ if succeeded by a vowel"
%{k%}:ᤁ <=> _ ( %>: ) ( :0 ) :Vowel ;
"{ll} becomes ᤗ if succeeded by a vowel"
%{ll%}:ᤗ <=> _ ( %>: ) ( :0 ) :Vowel ;
"{t} becomes ᤋ if succeeded by a vowel"
%{t%}:ᤋ <=> _ ( %>: ) ( :0 ) :Vowel ;
"{p} becomes ᤐ if succeeded by a vowel"
%{p%}:ᤐ <=> _ ( %>: ) ( :0 ) :Vowel ;

"ph becomes bh if preceded by a vowel"
%{BH%}:ᤓ <=> :Vowel ( %>: ) _ ;
"{B} becomes ᤒ if preceded by a vowel"
%{B%}:ᤒ <=> :Vowel ( %>: ) _ ;
"{ch} becomes ᤈ if preceded by a vowel"
%{ch%}:ᤈ <=> :Vowel ( %>: ) _ ;
"{T} becomes ᤍ if preceded by a vowel"
%{T%}:ᤍ <=> :Vowel ( %>: ) _ ;
"{K} becomes ᤃ if succeeded by a vowel"
%{K%}:ᤃ <=> _ %>: ( :᤻ ) ( :0 ) :Vowel ;
            :Vowel ( %>: ) _ ;
"{eg} becomes glottal stop if succeeded by consonant"
%{eg%}:᤹ <=> _ %>: :Consonant ;



! Resources:
! https://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! https://wiki.apertium.org/wiki/Twol
! https://wiki.apertium.org/wiki/Hfst

