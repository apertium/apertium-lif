# Morphological Transducer for Limbu

# Part of speech categories
# <n>      # Noun
# <v>      # Verb
# <adj>    # Adjective
# <num>    # Numeral

# <sent>   # Sentence-ending punctuation
# <guio>   # Hyphen
# <cm>     # Comma
# <apos>   # Apostrophe
# <quot>   # Quotation mark
# <lquot>  # Left quotation mark
# <rquot>  # Right quotation mark
# <lpar>   # Left parenthesis
# <rpar>   # Right parenthesis

# Number morphology
# <pl>     # Plural
# <sg>     # Singular
# <du>     # Dual

# Other symbols
# >        # Morpheme boundary

###############################################################################
###                       M O R P H O T A C T I C S                         ###
###############################################################################

PATTERNS
Number
Punctuation
Nouns
Verbs
Adjectives
Determiners-Words
Conjunctions
Adverbs
Question-Particles
CaseMarker
Pronoun-Words
DemonstrativePrns
DefinitivePrns
IndefinitePrns
InterrogativePrns
ReflexivePrns
Prepositions
Complementizer
Concession
Interjection
TopicMarker
Evidentiality
Postpositions
NumeralWords
Units [ <num>: ]
Ordinals [ <ord>: ]
EmphMarker

PATTERN Prepositions
PossessivePrn?(1) Prepos-Stems PossessivePrn?(2)

PATTERN NumeralWords
NumeralStems
NumeralStems ( [ +:> ]Adverbs)
NumeralStems Collectivity

PATTERN Postpositions
PossessivePrn?(1) Postpos-Stems PossessivePrn?(2) Cases?

PATTERN Interjection
Ij-Stem [ <ij>: ] ( [ +:> ]Question-Particles)?

PATTERN Adverbs
AdverbStem [ <adv>: ] ( [ +:> ]Adverbs2) [ <adv>: ] ( [ +:> ]Complementizer)?
AdverbStem [ <adv>: ] ( [ +:> ]Complementizer)?
AdverbStem [ <adv>: ] Concession
AdverbStem [ <adv>: ] Cases? Nominalizer

PATTERN Determiners-Words
(Adverbs [ +:> ] )? Determiners [ <det>: ] Comparative?

PATTERN Pronoun-Words
Pronouns PossessiveSuff? ( [ +:> ]Adverbs)?
Pronouns Cases?

PATTERN DemonstrativePrns
DemPrn Plurals? Cases?

PATTERN DefinitivePrns
DefPrn

PATTERN InterrogativePrns
IntPrn Nominalizer?

PATTERN IndefinitePrns
IndPrn

PATTERN ReflexivePrns
ReflPrn

PATTERN Nouns
(Adjectives [ +:> ] )? Voice?(1) PossessivePrn?(1) Noun-Stems Plurals Voice?(2) PossessivePrn?(2) Cases?
Noun-Stems Nominalizer Plurals ( [ +:> ] Adjectives)
Noun-Stems Nominalizer Imperious? Cases? ( [ +:> ] Adverbs)
(Adjectives [ +:> ] )? Noun-Stems Nominalizer
Noun-Stems Attr
Noun-Stems Cases Cases2 Plurals?
PossessivePrn?(1) Noun-Stems PossessivePrn?(2) Nominalizer? ToBeSuff

PATTERN Verbs
( (Adverbs [ +:> ] )? SubjPersonNumber(1) SubjPersonNumber(4) Negation?(1) Verb-Stems Negation?(2) Reflexivity&Port? Tense SubjPersonNumber(5) SubjPersonNumber(2) ObjSlot SubjPersonNumber(6) NegationSuff? SubjPersonNumber(7) ObjNumber SubjPersonNumber(8) SubjPersonNumber(3) NegationSuff2? Mode? Aspect Clitic? Nominalizer? Subord? ( [ +:> ] Adverbs)? )[^[past,nonpast],^[ssg1,ssg23],^[noport,bigport]]
( (Adverbs [ +:> ] )? SubjPersonNumber(1) SubjPersonNumber(4)? Negation?(1) Verb-Stems Negation?(2) Reflexivity&Port? Tense SubjPersonNumber(5)? SubjPersonNumber(2) ObjSlot SubjPersonNumber(6)? NegationSuff? SubjPersonNumber(7)? ObjNumber SubjPersonNumber(8)? SubjPersonNumber(3) NegationSuff2? Mode? Aspect Clitic? Nominalizer? Subord? ( [ +:> ] Adverbs)? )[^[past,nonpast],^[ssg1,ssg23],^[noport,bigport]]  # Dir/LR
Voice?(1) Negation?(1) Verb-Stems GerundMarker Negation?(2) Voice?(2) Cases? Subord? Concession? ( [ +:> ] Adverbs)?
Verb-Stems GerundMarker Imperious ( [ +:> ] Adverbs)?
Verb-Stems Nominalizer Cases? Subord
Verb-Stems Nominalizer Cases? Concession
Voice(1) Verb-Stems Voice(2) Voice(3) Cases? Subord
Voice(1) Verb-Stems Voice(2) Voice(3) Cases? Concession
Voice(1) Verb-Stems Voice(2) Voice(3) Cases Cases2
Verb-Stems GerundMarker ObjNumber|Reflexivity&Port ( [ +:> ] Adverbs)?
Verb-Stems Verb2 Concession
Verb-Stems GerundMarker Comparative
Verb-Stems ToBeSuff
(Conjunctions [ +:> ] ) Verb-Stems ObjSlot Cases
(DemonstrativePrns [ +:> ] ) Verb-Stems Tense SubjPersonNumber(2) ObjSlot ObjNumber Ergation

LEXICON PossessiveSuff
<pos>:>{gs}{i}{N}

LEXICON PossessivePrn(2)
:ᤀᤠ>               <px1sg>:
:ᤀᤠᤴᤆᤡ>         <px1duincl>:
:ᤀᤠᤴᤆᤡᤅᤠ>       <px1duexcl>:
:ᤀᤠᤏᤡ>          <px1plincl>:
:ᤀᤠᤏᤡᤅᤠ>        <px1plexcl>:
:ᤁᤠ>              <px2sg>:    # Dir/LR
:{K}{E}>         <px2sg>:
:ᤂᤧᤴᤆᤡ>            <px2du>:
:ᤂᤧᤏᤡ>             <px2pl>:
:ᤁᤢ>              <px3sg>:
:ᤂᤢᤴᤆᤡ>            <px3du>:
:ᤂᤢᤴᤆᤡ>            <px3pl>:

LEXICON ToBeSuff
<ser1sg>:>{gs}{Ee}
<ser1duincl>:>ᤛᤡ
<ser1plincl>:>ᤛᤡ
<ser1duexcl>:>ᤛᤡ>ᤃᤧ
<ser1plexcl>:>ᤛᤡ>ᤃᤧ
<ser2sg>:>ᤏᤧ
<ser2du>:>ᤏᤧᤆᤡ{ng}
<ser2du>:>ᤏᤧᤇᤡ{ng}
<ser2pl>:>ᤏᤡ{ng}
<ser3sg>:
<ser3du>:>ᤛᤡ
<ser3pl>:>ᤛᤡ

LEXICON Voice(3)
:{K}{E}>      <act>:        :>{Bb}{A}

LEXICON SubjPersonNumber(8)
:ᤀᤠ>[ssg1,noport]           <s_sg1>:[ssg1,noport]           <incl>:[ssg1,noport]            :[ssg1,noport]                :[ssg1,noport]           :>{ng}[ssg1,noport]         :[ssg1,noport]              :>{ng}[ssg1,noport]
:[ssg1,noport]             <s_sg1>:[ssg1,noport]           <incl>:[ssg1,noport]            :[ssg1,noport]                :[ssg1,noport]           :>{ng}[ssg1,noport]         :[ssg1,noport]              :>{ng}[ssg1,noport]
:ᤀᤠ>[ssg1,noport]           <s_sg1>:[ssg1,noport]           <excl>:>ᤃᤧ[ssg1,noport]          :[ssg1,noport]                :[ssg1,noport]           :>{ng}[ssg1,noport]         :[ssg1,noport]              :>{ng}[ssg1,noport]
:ᤀᤠ>[ssg23,noport]          <s_du1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤛ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]                 # Dir/LR
:ᤀᤠ>[ssg23,noport]          <s_du1>:[ssg23,noport]          <excl>:>ᤃᤧ[ssg23,noport]         :ᤔ{E}>[ssg23,noport]          :>ᤛ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]                 # Dir/LR
:ᤀᤠ>[ssg23,noport]          <s_du1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤇ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]
:ᤀᤠ>[ssg23,noport]          <s_du1>:[ssg23,noport]          <excl>:>ᤃᤧ[ssg23,noport]         :ᤔ{E}>[ssg23,noport]          :>ᤇ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]
:[ssg23,noport]            <s_du1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤛ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]                 # Dir/LR
:[ssg23,noport]            <s_du1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤇ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]
:ᤀᤠ>[ssg23,noport]          <s_pl1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:ᤀᤠ>[ssg23,noport]          <s_pl1>:[ssg23,noport]          <excl>:>ᤃᤧ[ssg23,noport]         :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:[ssg23,noport]            <s_pl1>:[ssg23,noport]          <excl>:>ᤃᤧ[ssg23,noport]         :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:[ssg23,bigport]           <s_pl1>:[ssg23,bigport]         <excl>:>[ssg23,bigport]         :ᤔ{E}>[ssg23,bigport]         :[ssg23,bigport]         :[ssg23,bigport]            :>{m}[ssg23,bigport]        :>{m}[ssg23,bigport]             # Dir/LR
:ᤀᤠ>[past,ssg23,noport]     <s_pl1>:[past,ssg23,noport]     <excl>:>ᤃᤧ[past,ssg23,noport]    :ᤔ{E}>[past,ssg23,noport]     :[past,ssg23,noport]     :[past,ssg23,noport]        :>ᤔᤏᤠ[past,ssg23,noport]     :[past,ssg23,noport]
:ᤀᤠ>[past,ssg23,bigport]    <s_pl1>:[past,ssg23,bigport]    <excl>:>[past,ssg23,bigport]    :ᤔ{E}>[past,ssg23,bigport]    :[past,ssg23,bigport]    :[past,ssg23,bigport]       :>ᤔᤏᤠ[past,ssg23,bigport]    :[past,ssg23,bigport]            # Dir/LR
:[ssg23,noport]            <s_pl1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:{K}{E}>[ssg23,noport]     <s_sg2>:[ssg23,noport]                :[ssg23,noport]           :[ssg23,noport]                :[ssg23,noport]         :[ssg23,noport]             :[ssg23,noport]              :[ssg23,noport]
:{K}{E}>[ssg23,noport]     <s_du2>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤛ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]                  # Dir/LR
:{K}{E}>[ssg23,noport]     <s_du2>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤇ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]
:{K}{E}>[ssg23,noport]     <s_pl2>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:[ssg23,noport]            <s_pl2>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:[ssg23,noport]            <s_sg3>:[ssg23,noport]                :[ssg23,noport]           :[ssg23,noport]               :[ssg23,noport]          :[ssg23,noport]             :[ssg23,noport]             :[ssg23,noport]
:[ssg23,noport]            <s_du3>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤛ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]                  # Dir/LR
:[ssg23,noport]            <s_du3>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤇ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]
:[ssg23,noport]            <s_pl3>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]

LEXICON Negation(2)
:ᤔᤧ>       <neg>:       # Dir/LR
:ᤔᤧ{N}>    <neg>:
:ᤏ>       <neg>:

LEXICON GerundMarker
<ger>:>ᤔᤢᤏᤠ
<inf>:>ᤔᤧ    # Dir/LR
<inf>:>ᤔᤠ

LEXICON Reflexivity&Port
<refl>:>ᤛᤡ{ng}
<refl>:>ᤏᤧ                # Dir/LR
<o_2>:>ᤏᤧ

LEXICON Tense
<nonpast>:[nonpast]
<past>:>{E}[past]
<past>:>{a}{E}[past]     # Dir/LR

LEXICON ObjSlot
<o_3>:>{a}{u}[noport]
<o_3>:>{u}[noport]       # Dir/LR
<os_pl>:>{a}{I}[noport]
<os_pl>:>{I}[noport]     # Dir/LR
<os_du>:>ᤛᤡ[noport]       # Dir/LR
<os_du>:>ᤇᤡ[noport]
<os_sg>:[noport]

<os_sg12>:>ᤧ[nonpast,noport]
<o_3>:>ᤧ[nonpast,ssg1,noport]
<os_sg12>:>{a}{A}{ng}[past,noport]
<os_sg12>:>{A}{ng}[past,noport]       # Dir/LR
<o_3>:>ᤐᤠ{ng}[past,ssg1,noport]
<​o_3>:>{a}{u}{m}ᤛᤡᤶᤒᤧ[bigport]
<​o_3>:>{u}{m}ᤛᤡᤶᤒᤧ[bigport]            # Dir/LR

LEXICON NegationSuff
<neg>:>{N}

LEXICON Mode
<opt>:>ᤖ
<opt>:>ᤗ   # Dir/LR
<cni>:>ᤔᤧ{N}
<irr>:>ᤃᤏᤡ

LEXICON Aspect
<impf>:>{B}{A}
<perf>:

LEXICON Clitic
<clt><ass>:>ᤖᤥ
<clt><ass>:>ᤗᤥ   # Dir/LR
<clt><depr>:>ᤗᤇ  # Dir/LR
<clt><depr>:>ᤖᤇ

LEXICON ObjNumber
<o_nonsg>:>ᤛᤡ[noport]
<o_nonsg>:[bigport]      # Dir/LR
<o_sg>:[noport]

LEXICON NegationSuff2
<neg>:>{N}
<neg>:>ᤏᤧ{N} # Dir/LR

LEXICON Plurals
<sg>:
<pl>:>ᤄᤠ
<pl>:>ᤜᤠ᤹ # Dir/LR
<du>:>ᤄᤠᤇᤡ
<du>:>ᤜᤠ᤹ᤇᤡ # Dir/LR

LEXICON Cases
<nom​>:>ᤒᤧ
<nom>:>ᤒᤣ
<gen>:>{NG}{A}{n}
<gen​>:>ᤖᤧ              # Dir/LR
<gen​>:>ᤗᤧ              # Dir/LR
<gen>:>{l}ᤗᤧ           # Dir/LR
<gen>:>{ie}{l}ᤗᤧ       # Dir/LR
<gen>:>{a}{ie}{l}ᤗᤧ    # Dir/LR
<gen>:>ᤖᤣ              # Dir/LR
<gen>:>ᤗᤣ              # Dir/LR
<gen>:>{l}ᤗᤣ           # Dir/LR
<gen>:>{ie}{l}ᤗᤣ       # Dir/LR
<gen>:>{a}{ie}{l}ᤗᤣ    # Dir/LR
<voc>:>{aa}{o}
<voc>:>{ana}{e}       # Dir/LR
<voc>:>ᤖᤧ              # Dir/LR
<voc>:>ᤖᤣ              # Dir/LR
<imp>:>{Ee}
<loc>:>{oo}
<med>:>ᤗᤠ{m}
<ela>:>{oo}ᤏᤢ
<ela>:>{oo}ᤗᤠ{m}       # Dir/LR
<all>:>ᤌᤠᤰ
<all>:>ᤎᤠᤰ             # Dir/LR
<itrt>:>ᤗᤢᤔᤥ
<itrt>:>ᤗᤢᤶᤔᤥ          # Dir/LR
<comp>:>ᤏᤢ{do}{l}ᤗᤧ
<comp>:>ᤏᤢ{do}{l}ᤗᤣ    # Dir/LR
<com>:>ᤏᤢ{ng}
<com>:>ᤏᤢ              # Dir/LR
<abl>:>ᤗᤠ{m}
<abl>:>ᤏᤢ{ng}          # Dir/LR
<erg>:>ᤖᤧ
<erg>:>ᤗᤧ              # Dir/LR
<erg>:>{l}ᤗᤧ           # Dir/LR
<erg>:>{ie}{l}ᤗᤧ       # Dir/LR
<erg>:>{a}{ie}{l}ᤗᤧ    # Dir/LR
<erg>:>ᤖᤣ              # Dir/LR
<erg>:>ᤗᤣ              # Dir/LR
<erg>:>{l}ᤗᤣ           # Dir/LR
<erg>:>{ie}{l}ᤗᤣ       # Dir/LR
<erg>:>{a}{ie}{l}ᤗᤣ    # Dir/LR
<abs>:>{MNG}{Iabs}{N}
<abs>:>{MNG}{EGS}{N}  # Dir/LR
<abs​>:>{N}            # Dir/LR
<ins>:>ᤗᤧ
<ins>:>{l}ᤗᤧ           # Dir/LR
<ins>:>{i}{l}ᤗᤧ        # Dir/LR
<ins>:>ᤗᤣ              # Dir/LR
<ins>:>{l}ᤗᤣ           # Dir/LR
<ins>:>{i}{l}ᤗᤣ        # Dir/LR

ALIAS Cases Cases2

LEXICON Ergation
<erg>:>ᤖᤧ
<erg>:>ᤗᤧ              # Dir/LR
<erg>:>{l}ᤗᤧ           # Dir/LR
<erg>:>{ie}{l}ᤗᤧ       # Dir/LR
<erg>:>{a}{ie}{l}ᤗᤧ    # Dir/LR
<erg>:>ᤖᤣ              # Dir/LR
<erg>:>ᤗᤣ              # Dir/LR
<erg>:>{l}ᤗᤣ           # Dir/LR
<erg>:>{ie}{l}ᤗᤣ       # Dir/LR
<erg>:>{a}{ie}{l}ᤗᤣ    # Dir/LR

LEXICON Attr
<attr>:

LEXICON Subord
<sub>:>{ie}{l}ᤗᤧ
<sub>:>{l}ᤗᤧ           # Dir/LR
<sub>:>ᤗᤧ              # Dir/LR
<sub​>:>ᤗᤠ{do}ᤃᤡ

LEXICON Imperious
<def>:>{E}{N}

LEXICON Nominalizer
<nom>:>{Bb}{A}

LEXICON Comparative
<comp>:>ᤏᤢ{do}{l}ᤗᤧ
<comp>:>ᤏᤢ{do}{l}ᤗᤣ    # Dir/LR

LEXICON Collectivity
<cnt>:>{BH}{u}

###############################################################################
###                          L E X I C O N                                  ###
###############################################################################

LEXICON Pronouns
ᤀᤠ<prn><pers><p1><sg>:ᤀᤠᤱᤃᤠ{gs}
ᤀᤠ<prn><pers><p1><sg>:ᤅᤠ              # Dir/LR
ᤀᤠ<prn><pers><p1><sg>:ᤀᤡᤱᤃᤠ{gs}         # Dir/LR
ᤀᤠ<prn><pers><p1><du><incl>:ᤀᤠᤴᤇᤡ{gs}
ᤀᤠ<prn><pers><p1><du><excl>:ᤀᤠᤴᤇᤡ>ᤃᤧ
ᤀᤠ<prn><pers><p1><du><excl>:ᤀᤠᤴᤇᤡ>ᤃᤩᤠ    # Dir/LR
ᤀᤠ<prn><pers><p1><du><excl>:ᤀᤠᤴᤇᤡᤅᤠ     # Dir/LR
ᤀᤠ<prn><pers><p1><pl><incl>:ᤀᤠᤏᤡ{gs}
ᤀᤠ<prn><pers><p1><pl><excl>:ᤀᤠᤏᤡ>ᤃᤧ
ᤀᤠ<prn><pers><p1><pl><excl>:ᤀᤠᤏᤡ>ᤃᤣ     # Dir/LR
ᤀᤠ<prn><pers><p1><pl><excl>:ᤀᤠᤏᤡ>ᤃᤩᤠ     # Dir/LR
ᤀᤠ<prn><pers><p1><pl><excl>:ᤀᤠᤏᤡᤅᤠ      # Dir/LR

ᤂᤧᤏᤧ<prn><pers><p2><sg>:ᤂᤧᤏᤧ{gs}
ᤂᤧᤴᤆᤡ<prn><pers><p2><du>:ᤂᤧᤴᤇᤡ
ᤂᤧᤏᤡ<prn><pers><p2><pl>:ᤂᤧᤏᤡ{gs}
ᤂᤡᤏᤡ<prn><pers><p2><pl>:ᤂᤡᤏᤡ{gs}        # Dir/LR
ᤁᤴ<prn><pers><p3><sg>:ᤁᤴ
ᤂᤢᤏᤧ<prn><pers><p3><mf><sg>:ᤂᤢᤏᤧ{gs}
ᤂᤧᤱ<prn><pers><p3><nt><sg>:ᤂᤧᤱ
ᤂᤢᤴᤆᤡ<prn><pers><p3><du>:ᤂᤢᤴᤇᤡ
ᤂᤧᤱᤜᤠ<prn><pers><p3><pl>:ᤂᤧᤱᤜᤠ
ᤂᤢᤏᤡ<prn><pers><p3><pl>:ᤂᤢᤏᤡ{gs}        # Dir/LR
ᤂᤢᤴᤆᤡ<prn><pers><p3><nonsg>:ᤂᤢᤴᤇᤡ{gs}   # Dir/LR
ᤂᤧᤱᤜᤠ<prn><pers><p3><nonsg>:ᤂᤧᤱᤜᤠ        # Dir/LR

LEXICON DemPrn
ᤂᤴ<prn><dem>:ᤂ{N}
ᤂᤧᤴ<prn><dem>:ᤂᤧ{N}
ᤂᤧᤱ<prn><dem>:ᤂᤧ{ng} # Dir/LR
ᤁᤱ<prn><dem>:ᤁ{ng}

LEXICON DefPrn
ᤂᤴᤏᤧᤴ<prn><def>:ᤂᤴᤏ>{E}{N}

LEXICON IntPrn
ᤌᤣ<prn><itg>:ᤌᤣ
ᤌᤧᤔᤠ<prn><itg>:ᤌᤧᤔᤠ
ᤌᤣᤔᤠ<prn><itg>:ᤌᤣᤔᤠ
ᤜᤠᤔᤠ<prn><itg>:ᤜᤠᤔᤠ
ᤜᤣᤔᤠ<prn><itg>:ᤜᤣᤔᤠ
ᤜᤧᤴ<prn><itg>:ᤜᤧᤴ
ᤀᤣ᤺ᤴ<prn><itg>:ᤀᤣ᤺{N}
ᤀᤣᤏ᤻<prn><itg>:ᤀᤣᤏ᤻     # Dir/LR
ᤀᤣ᤺ᤸᤗᤣ<prn><itg>:ᤀᤣ᤺ᤸᤗᤣ
ᤀᤣᤗ᤻ᤗᤣ<prn><itg>:ᤀᤣᤗ᤻ᤗᤣ  # Dir/LR
ᤀᤠᤳᤋᤡ<prn><itg>:ᤀᤠᤳᤋᤡ
ᤀᤠᤳᤋᤡᤴ<prn><itg>:ᤀᤠᤳᤋᤡ{N} 

LEXICON IndPrn
ᤁᤢᤓᤠ<prn><ind>:ᤁᤢᤓᤠ

LEXICON ReflPrn
ᤀᤠᤒᤠᤅᤣ<prn><ref>:ᤀᤠᤒᤠ>ᤅᤣ

LEXICON Prepos-Stems
ᤌᤥ<pr>:ᤌᤥ
ᤏᤠ<pr>:ᤏ{A}
ᤀᤧᤶᤓᤧᤍᤠᤱᤒᤠ<pr>:ᤀᤧᤶᤓᤧᤍᤠᤱ>ᤒ{Aabs}
ᤗᤠᤃᤡ<pr>:ᤗᤠᤃᤡ
ᤑᤠ᤹ᤀᤠᤱ<pr>:ᤑᤠ᤹ᤀᤠ{ng}
ᤌᤠᤖᤡᤰ<pr>:ᤌᤠᤖᤡ{k}
ᤋᤃᤡ<pr>:ᤋᤃᤡ
ᤗᤧᤵᤔᤠᤱ<pr>:ᤗᤧᤵᤔᤠ{ng}
ᤁᤢᤛᤡᤱ<pr>:ᤁᤢᤛᤡ{ng}
ᤛᤥᤖᤡᤰ:ᤛᤥᤖᤡ{k}

LEXICON Postpos-Stems
ᤛᤡᤰᤋᤥ:ᤛᤡᤰᤋᤥ
ᤀᤡᤛᤡᤰ:ᤀᤡᤛᤡ{k}{do}
ᤀᤡᤛᤡᤁ᤻:ᤀᤡᤛᤡᤁ{km}    # Dir/LR

LEXICON TopicMarker
ᤆᤀᤡᤲ<top>:ᤆᤀᤡᤲ
ᤁ<top>:ᤁ
ᤛᤧᤴ<top>:ᤛᤧ{N}

LEXICON Complementizer
ᤑᤧᤸᤗᤧ<comp>:{BH}{E}{l}ᤗᤧ

LEXICON Adjectives
ᤏᤢᤒᤠ<adj><m>:ᤏᤢ>ᤒ{A}
ᤏᤢ<adj>:ᤏᤢ
ᤀᤠᤰᤂᤣ<adj>:ᤀᤠᤰᤂ{eg}
ᤀ᤹ᤍ᤺ᤱ<adj>:ᤀ᤹ᤍ᤺{ng}
ᤆᤢᤰᤛᤠ<adj><dim>:ᤆᤢᤰ>ᤛ{A}
ᤂᤶᤍᤠᤱ<adj>:ᤂᤶᤍᤠ{ng}
ᤋᤠᤃᤧᤖᤠ<adj>:ᤋᤠᤃᤧᤖ{A}
ᤘᤣ᤹<adj>:ᤘᤣ{gs}
ᤗᤳᤇᤠ<adj>:ᤗᤳᤇ{A}
ᤕᤶᤒᤠ<adj><m>:ᤕᤶ>ᤒ{A}
ᤕᤖᤡᤰ<adj>:ᤕᤖᤡ{k}
ᤌᤣᤀᤠᤱ<adj>:ᤌᤣᤀᤠ{ng}
ᤜᤠᤱ<adj>:ᤜᤠ{ng}
ᤁᤢᤓᤠ<adj>:ᤁᤢᤓ{A}
ᤁᤠᤰ<adj>:ᤁᤠ{k}

LEXICON CaseMarker
ᤋᤠᤵ<nom>:ᤋᤠ{p}
ᤖᤧ<erg>:ᤖᤧ
ᤗᤧ<erg>:ᤗᤧ        # Dir/LR

LEXICON Concession
ᤛᤠᤱ<concess>:ᤛᤠ{ng}

LEXICON Question-Particles
ᤀᤡ<qst>:{a}{I}
ᤀᤡ<qst>:{I}      # Dir/LR

LEXICON Determiners
ᤁᤧᤖᤧᤰ:ᤁᤧᤖᤧ{k}
ᤕᤸᤗᤡᤰ:ᤕᤸᤗᤡ{k}
ᤈᤶᤔ:ᤈᤶᤔ
ᤂ:ᤂ
ᤀᤖᤢ:ᤀᤖᤢ
ᤍᤠᤱᤒᤧᤸᤗᤧ:ᤍᤠᤱᤒᤧᤸᤗᤧ    #unsure
ᤍᤠᤱᤒᤠᤸᤗᤧ:ᤍᤠᤱᤒᤠᤸᤗᤧ    #unsure

LEXICON Ij-Stem
ᤛᤣᤘᤠᤖᤥ:ᤛᤣᤘᤠᤖᤥ
ᤛᤧᤘᤠᤖᤥ:ᤛᤧᤘᤠᤖᤥ
ᤔᤧ᤺ᤴ:ᤔᤧ᤺{N}
ᤗᤥ:ᤗᤥ

LEXICON Conjunctions
ᤋᤖ<cnjcoo>:ᤋᤖ
ᤑᤃᤖ<cnjsub>:ᤑᤃᤖ
ᤜᤧᤰᤁᤩᤠᤱ<cnjcoo>:ᤜᤧᤰᤁᤩᤠ{ng}
ᤁᤖ<cnj>:ᤁᤖ
ᤑᤠ᤹ᤀᤠᤱ<cnj>:ᤑᤠ᤹ᤀᤠ{ng}
ᤀᤡᤃᤪ<cnjcoo>:ᤀᤡᤃᤪ
ᤏᤢ<cnjcoo>:ᤏᤢ
ᤂᤧᤸᤗᤧ<cnjcoo>:ᤂᤧᤸᤗᤧ

LEXICON Evidentiality
ᤗᤇ<evid>:ᤗᤇ
ᤖᤇ<evid>:ᤖᤇ

LEXICON EmphMarker
ᤏᤠ<emph>:ᤏᤠ

LEXICON AdverbStem
ᤂᤶᤓ:ᤂᤶᤓ
ᤂᤶᤓᤧ:ᤂᤶᤓᤧ           # Dir/LR
ᤂᤶᤓᤠ:ᤂᤶᤓᤠ           # Dir/LR
ᤀᤧᤶᤓᤧᤸᤗᤧ:ᤀᤧᤶᤓᤧᤸᤗᤧ
ᤕᤠᤶᤔᤢ:ᤕᤠᤶᤔᤢ
ᤕᤠᤶᤔᤥ:ᤕᤠᤶᤔᤥ          # Dir/LR
ᤖᤳ:ᤖ{t}
ᤀᤠᤱ:{a}{A}{ng}
ᤀᤠᤱ:{A}{ng}         # Dir/LR
ᤀᤠᤓᤣᤍᤠᤱᤒᤠ:ᤀᤠᤓᤣᤍᤠᤱᤒᤠ
ᤀᤠᤓᤣᤍᤠᤱ:ᤀᤠᤓᤣᤍᤠᤱ
ᤀᤠᤸᤗᤠ:ᤀᤠᤸᤗᤠ
ᤔᤧ᤺ᤴᤏ:ᤔᤧ᤺ᤴᤏ
ᤔᤧᤏ᤻ᤏ:ᤔᤧᤏ᤻ᤏ # Dir/LR
ᤌᤧᤕᤠ᤺ᤴᤍᤰ:ᤌᤧᤕᤠ᤺ᤴᤍ{k}
ᤌᤧᤕᤠᤏ᤻ᤍᤰ:ᤌᤧᤕᤠᤏ᤻ᤍ{k}     # Dir/LR
ᤀᤄᤡ:ᤀᤄᤡ
ᤂᤶᤓᤧᤸᤗᤧ:ᤂᤶᤓᤧᤸᤗᤧ
ᤀᤠᤓᤣ:ᤀᤠᤓᤣ
ᤀᤰᤂᤣ:ᤀᤰᤂᤣ
ᤏᤢᤖᤡᤰ:ᤏᤢᤖᤡ{k}
ᤀᤳᤋᤡ:ᤀᤳᤋᤡ
ᤜᤧᤰᤁᤩᤠᤱ:ᤜᤧᤰᤁᤩᤠ{ng}
ᤀᤠᤸᤗ:ᤀᤠᤸᤗ
ᤛᤠ᤹ᤖᤡᤰ:ᤛᤠ{gs}ᤖᤡ{k}
ᤆᤡ:ᤆᤡ
ᤜᤧᤰᤁᤧ:ᤜᤧᤰᤁᤧ
ᤜᤧᤰᤁᤣ:ᤜᤧᤰᤁᤣ  # Dir/LR
ᤌᤣᤀᤠᤱ:ᤌᤣᤀᤠ{ng}
ᤀᤣᤃᤠᤱ:ᤀᤣᤃᤠ{ng}
ᤀᤠᤳᤋᤡ:ᤀᤠᤳᤋᤡ
ᤛᤍᤣ:ᤛᤍᤣ
ᤛᤍᤠᤍᤡᤱ:ᤛᤍᤠᤍᤡ{ng}
ᤂᤧᤒᤩᤠᤱ:ᤂᤧᤒᤩᤠᤱ
ᤂᤧᤵᤔᤥ:ᤂᤧᤵᤔᤥ
ᤀᤠᤵᤑᤣᤰ:ᤀᤠᤵᤑᤣ{k}

ALIAS AdverbStem Adverbs2

LEXICON Verb-Stems
ᤀᤡᤶᤔᤠ<v>:ᤀᤡ{P}{S}
ᤗᤡᤶᤔᤠ<v>:ᤗᤡᤶ{D}
ᤛᤥ᤺ᤔᤠ<v>:ᤛᤥ᤺{S}
ᤖᤠ᤺ᤰᤔᤠ<v>:ᤖᤠ᤺ᤰ{s}
ᤖᤠᤁ᤻ᤔᤠ<v>:ᤖᤠᤁ᤻{s}              # Dir/LR
ᤗᤠᤁ᤻ᤔᤠ<v>:ᤗᤠᤁ᤻{s}              # Dir/LR
ᤗᤠ᤺ᤰᤔᤠ<v>:ᤗᤠ᤺ᤰ{s}              # Dir/LR
ᤗᤠ᤺ᤰᤔᤠ<v>:ᤗᤠ᤺ᤰ{S}              # Dir/LR
ᤗᤠᤁ᤻ᤔᤠ<v>:ᤗᤠᤁ᤻{S}              # Dir/LR
ᤐᤣ᤺ᤰᤔᤠ<v>:{B}{e}{do}{K}
ᤐᤣᤁ᤻ᤔᤠ<v>:{B}{e}{K}{km}      # Dir/LR
ᤋᤠ᤺ᤔᤠ<v>:{T}{do}
ᤏᤡᤔᤠ<v>:ᤏᤡ{So}
ᤘᤠᤔᤠ<v>:ᤘ{ay}{gs}
ᤛᤧᤶᤔᤠ<v>:ᤛᤧ{do}{R}
ᤆᤥ᤺ᤰᤔᤠ<vbdo>:{ch}{oo}{do}{Kk}
ᤆᤥᤁ᤻ᤔᤠ<vbdo>:{ch}{K}{km}     # Dir/LR
ᤆᤠᤔᤠ<v>:ᤆ{ao}
ᤐᤥ᤺ᤱᤔᤠ<v>:{B}{oo}{do}{NGK}{S}
ᤐᤥᤅ᤻ᤔᤠ<v>:{B}{oo}ᤅ{km}{S}  # Dir/LR
ᤐᤠ᤺ᤵᤔᤠ<v>:{B}{A}{do}{pgt}{R}
ᤐᤠᤐ᤻ᤔᤠ<v>:{B}{A}ᤐ{km}{R}    # Dir/LR
ᤔᤠ᤺ᤔᤠ<v>:ᤔᤠ{do}{S}
ᤗ᤺ᤵᤔᤠ<v>:ᤗ᤺{tp}{tm}
ᤗᤐ᤻ᤔᤠ<v>:ᤗ{t}{km}{tm}
ᤗ᤺ᤶᤔᤠ<v>:ᤗ{do}{NM}{Dnull}
ᤀᤢ᤺ᤵᤔᤠ<v>:ᤀᤢ{do}{tp}{tm}
ᤀᤢᤐ᤻ᤔᤠ<v>:ᤀᤢᤐ{km}{tm}        # Dir/LR
ᤑᤧ᤹ᤔᤠ<v>:ᤑᤧ{dg}{N}
ᤋᤧᤶᤔᤠ<v>:ᤋᤧ{t}
ᤋᤥ᤺ᤔᤠ<v>:ᤋᤥ{do}
ᤏᤠᤘᤠᤔᤠ<v>:ᤏᤠᤘᤠ          # unsure
ᤋᤠ᤹ᤔᤠ<v>:ᤋ{R}{do}{ng}  # unsure
ᤔᤠᤛᤡᤱᤔᤠ<v>:ᤔᤠᤛᤡᤱ{gs}
ᤁᤠᤶᤔᤠ<v>:ᤁᤠ{m}
ᤔᤡᤈᤠ᤹ᤔᤠ<v>:ᤔᤡᤈᤠ{gs}{rr}
ᤗᤧᤵᤔᤠ<v>:ᤗᤧ{gs}{rtl}
ᤏᤠ᤺ᤰᤔᤠ<v>:ᤏᤠ{do}{k}{tnull}
ᤌᤢᤱᤔᤠ<v>:{th}{u}{ng}{snull}
ᤗᤠᤱᤄᤣ᤺ᤰᤔᤠ<v>:ᤗᤠᤱᤄᤣ{do}{gs}{kg}
ᤗᤠᤱᤄᤣᤁ᤻ᤔᤠ<v>:ᤗᤠᤱᤄᤣᤁ{km}{gs}{kg}
ᤗᤧ᤺ᤔᤠ<v>:ᤗᤧ{do}
ᤛᤢᤰᤔᤠ<v>:ᤛᤢ{k}{tnull}
ᤜᤠ᤺ᤵᤔᤠ<v>:ᤜᤠ{do}
ᤗ᤹ᤔᤠ<vbdo>:ᤗ᤹
ᤔᤧᤵᤔᤠ<v>:ᤔᤧ{tp}{t}

ALIAS Verb-Stems Verb2

LEXICON Noun-Stems
ᤐᤢ<n>:ᤐᤢ
ᤋᤱ<n>:ᤋᤱ
ᤀᤠᤶᤒᤣ<n>:ᤀᤠᤶᤒᤣ
ᤛᤠᤵᤗᤠ<n>:ᤛᤠᤵᤗᤠ
ᤖᤠᤔ᤻<n>:ᤖᤠᤔ᤻ # Dir/LR
ᤏᤠᤰ<n>:ᤏᤠ{k}
ᤔᤣᤴᤇᤠ<n>:ᤔᤣᤴᤇᤠ
ᤗᤠᤶᤛ᤹<n>:ᤗᤠᤶᤛ᤹
ᤏᤠᤵᤔᤡ<n>:ᤏᤠᤵᤔᤡ
ᤂᤩᤠ<n>:ᤂᤩᤠ
ᤀᤕᤰᤐᤠ<n>:ᤀᤕᤰ>ᤐ{Aabs}
ᤁᤖᤁᤘᤠ<n>:ᤁᤖᤁᤘᤠ
ᤔᤠ<n>:ᤔ{A}
ᤐᤠ<n>:ᤐ{A}
ᤐᤡᤋ<n>:ᤐᤡᤋ
ᤀᤡᤗᤠᤶ<n>:ᤀᤡᤗᤠ{m}
ᤛᤣᤒᤠ<n>:ᤛᤣᤒᤠ
ᤆᤢᤶ<n>:{ch}{u}{m}
ᤋᤣ᤺ᤱ<n>:ᤋᤣ᤺{ng}
ᤋᤣᤅ᤻<n>:ᤋᤣᤅ᤻ # Dir/LR
ᤁᤠᤌᤔᤠᤴᤍᤢ<n>:ᤁᤠᤌᤔᤠᤴᤍᤢ
ᤐᤩᤠᤱᤛᤡ<n>:ᤐᤩᤠᤱᤛᤡ
ᤕᤠᤰᤌᤢᤱ<n>:ᤕᤠᤰᤌᤢ{ng}
ᤕᤠ᤹<n>:ᤕᤠ᤹
ᤆᤱ<n>:ᤆ{ng}
ᤕᤠᤰᤌᤢᤶᤒᤠ<n>:ᤕᤠᤰᤌᤢᤶ>ᤒ{Aabs}
ᤐᤧᤏᤡ<n>:ᤐᤧᤏ{i}
ᤕᤠᤖᤡᤰ<n>:ᤕᤠᤖᤡ{k}
ᤒᤡᤆᤠᤖ<n>:ᤒᤡᤆᤠᤖ
ᤐᤠᤴ<n>:ᤐᤠ{do}{N}
ᤐᤠᤏ᤻<n>:ᤐᤠ{do}{N}  # Dir/LR
ᤔᤧᤴᤆᤡᤀᤠ<n>:ᤔᤧᤴᤆᤡᤀᤠ
ᤔᤏᤠ<n>:ᤔᤏᤠ{do}
ᤏᤠᤖᤠ<n>:ᤏᤠᤖᤠ
ᤛᤥᤖ<n>:ᤛᤥᤖ
ᤀ᤹ᤍ᤺ᤱ<n>:ᤀ᤹ᤍ᤺{ng}
ᤏᤢ<n>:ᤏᤢ
ᤁᤢᤶᤔᤠ<n>:ᤁᤢᤶᤔ{A}
ᤁᤏᤠᤶ<n>:ᤁᤏᤠ{m}
ᤁᤰ<n>:ᤁ{k}
ᤁᤰᤁᤓᤡᤰᤏᤠ<n>:ᤁᤰᤁᤓᤡᤰᤏ{A}
ᤁᤰᤑᤠ<n>:ᤁᤰᤑ{A}
ᤁᤰᤑᤣ᤹<n>:ᤁᤰᤑᤣ᤹
ᤁᤰᤗᤠ<n>:ᤁᤰᤗ{A}
ᤁᤰᤗᤡᤰᤔᤢᤕᤠ<n>:ᤁᤰᤗᤡᤰᤔᤢᤕ{A}
ᤁᤰᤗᤡᤰᤘᤠ<n>:ᤁᤰᤗᤡᤰᤘ{A}
ᤁᤰᤗᤡᤎᤡ᤺ᤶᤒᤠ<n>:ᤁᤰᤗᤡᤎᤡ᤺ᤶᤒ{A}
ᤁᤰᤗᤡᤎᤡᤔ᤻ᤒᤠ<n>:ᤁᤰᤗᤡᤎᤡᤔ᤻ᤒ{A}  # Dir/LR
ᤁᤰᤗᤧᤰᤂᤣ<n>:ᤁᤰᤗᤧᤰᤂᤣ
ᤋᤖ᤺ᤱ<n>:ᤋᤖ᤺{ng}
ᤋᤖᤅ᤻<n>:ᤋᤖ{ng}{km}    # Dir/LR
ᤋᤠᤱᤛᤠᤱ<n>:ᤋᤠᤱᤛᤠ{ng}
ᤀᤡᤰᤛᤠᤍᤡᤱ<n>:ᤀᤡᤰᤛᤠᤍᤡ{ng}
ᤀᤡᤰᤛᤠ<n>:ᤀᤡᤰᤛ{A}
ᤂᤠᤶᤒᤣ᤺ᤰ<n>:ᤂᤠᤶᤒᤣ᤺{k}
ᤆᤀᤡ᤺ᤳ<n>:ᤆᤀᤡ{do}{t}
ᤏᤠᤘᤠ<n>:ᤏᤠᤘ{A}
ᤏᤡᤱᤘᤠ᤹<n>:ᤏᤡᤱᤘᤠ{gs}
ᤕᤠ᤺ᤶᤒᤰ<n>:ᤕᤠ᤺ᤶᤒ{k}
ᤕᤠᤔ᤻ᤒᤰ<n>:ᤕᤠᤔ᤻ᤒ{k}       # Dir/LR
ᤜᤠᤱ<n>:ᤜᤠ{ng}
ᤜᤠᤱ<n>:ᤜᤠ{ng}{ng}      # Dir/LR
ᤔᤧᤴᤇᤠᤶ<n>:ᤔᤧᤴᤇᤠ{m}
ᤀᤡᤛᤪᤠᤀᤡᤗᤒᤠ<n>:ᤀᤡᤛᤪᤠᤀᤡᤗ>ᤒ{Aabs}
ᤁᤠᤰ<n>:ᤁᤠ{k}
ᤕᤠᤵᤔᤡ<n>:ᤕᤠᤵᤔᤡ
ᤐᤥᤱ<n>:ᤐᤥ{do}{ng}{gs}
ᤔᤠᤱᤏᤡᤱᤘᤠ᤹ᤐᤠᤴ<n>:ᤔᤠᤱᤏᤡᤱᤘᤠ᤹>ᤐᤠ{N}
ᤔᤠᤱᤏᤡᤱᤘᤠ᤹ᤒᤠ<n>:ᤔᤠᤱᤏᤡᤱᤘᤠ᤹>ᤒ{Aabs}
ᤛᤡᤍᤢᤱᤈᤓᤠ<n>:ᤛᤡᤍᤢᤱᤈᤓ{A}
ᤛᤡᤍᤢᤱᤆᤓᤠ<n>:ᤛᤡᤍᤢᤱᤆᤓ{A}
ᤋᤢᤰᤂᤣ<n>:ᤋᤢᤰᤂᤣ
ᤗᤧᤴᤍᤡᤰ<n>:ᤗᤧᤴᤍᤡ{k}{do}
ᤗᤧᤴᤍᤡᤁ᤻<n>:ᤗᤧᤴᤍᤡᤁ{km}
ᤗᤧᤴ<n>:ᤗᤧ{N}
ᤛᤧᤴᤍᤡᤰ<n>:ᤛᤧᤴᤍᤡ{k}{do}
ᤛᤧᤴᤍᤡᤁ᤻<n>:ᤛᤧᤴᤍᤡᤁ{km}
ᤛᤧᤴ<n>:ᤛᤧ{N}
ᤍᤠᤱᤒᤠ<n>:ᤍᤠᤱ>ᤒ{Aabs}
ᤗᤢᤱᤔᤠ᤹<n>:ᤗᤢᤱᤔᤠ᤹

# names of god
ᤀᤸᤗᤧᤰᤂᤠ<n>:ᤀᤸᤗᤧᤰᤂᤠ
ᤀᤸᤗᤧᤰᤂᤠᤔᤠᤱ<n>:ᤀᤸᤗᤧᤰᤂᤠᤔᤠ{ng}
ᤀᤠᤗᤧᤰᤂᤠᤔᤠᤱ<n>:ᤀᤠᤗᤧᤰᤂᤠᤔᤠ{ng}
ᤋᤥᤗᤡᤱᤛᤥᤔᤢ<n>:ᤋᤥᤗᤡᤱᤛᤥᤔᤢ
ᤏᤡᤱᤘᤠ᤹ᤑᤢᤔᤠ<n>:ᤏᤡᤱᤘᤠ᤹ᤑᤢᤔ{A}
ᤏᤡᤱᤘᤠ᤹ᤑᤢ<n>:ᤏᤡᤱᤘᤠ᤹ᤑᤢ
ᤏᤡᤱᤘᤠ᤹ᤑᤢᤔᤠᤱ<n>:ᤏᤡᤱᤘᤠ᤹ᤑᤢᤔᤠ{ng}
ᤏᤡᤱᤘᤠ᤹ᤑᤢᤔᤠᤱ<n>:ᤏᤡᤱᤘᤠ᤹ᤑᤢᤔᤠ{ng}{ng}   # Dir/LR
ᤆᤠ᤺ᤔᤠᤱ<n>:ᤆᤠ᤺ᤔᤠ{ng}
ᤕᤜᤘᤣ<n>:ᤕᤜᤘᤣ
ᤕᤧᤛᤢ<n>:ᤕᤧᤛᤢ

# time Words
ᤐᤡᤴᤆᤧᤵ<n>:ᤐᤡᤴᤆᤧ{p}
ᤐᤣᤔᤢᤰ<n>:ᤐᤣ>ᤔᤢ{k}
ᤁᤢᤔᤢᤰ<n>:ᤁᤢ>ᤔᤢ{k}
ᤕᤧᤴ<n>:ᤕᤧ{N}
ᤕᤧᤴᤃᤠᤵ<n>:ᤕᤧᤴ>ᤃᤠ{p}
ᤗᤠ<n>:ᤗ{A}
ᤋᤱᤒᤣ<n>:ᤋᤱᤒᤣ

# days of the week
ᤋᤧᤏᤧᤒᤠ<n>:ᤋᤧᤏᤧ>ᤒ{Aabs}
ᤑᤥᤖᤠᤒᤠ<n>:ᤑᤥᤖᤠ>ᤒ{Aabs}
ᤔᤡᤰᤜᤠᤱᤒᤠ<n>:ᤔᤡᤰᤜᤠᤱ>ᤒ{Aabs}
ᤂᤣᤰᤛᤢᤒᤠ<n>:ᤂᤣᤰᤛᤢ>ᤒ{Aabs}
ᤁᤢᤖᤢᤵᤐᤠ<n>:ᤁᤢᤖᤢᤵ>ᤐ{Aabs}
ᤑᤛᤢᤵᤐᤠ<n>:ᤑᤛᤢᤵ>ᤐ{Aabs}
ᤛᤧᤴᤇᤪᤧᤱᤒᤠ<n>:ᤛᤧᤴᤇᤪᤧᤱ>ᤒ{Aabs}

# seasons
ᤜᤠᤱᤘᤠ᤺ᤶᤔᤠ<n>:ᤜᤠᤱ>ᤘᤠ᤺ᤶᤔ{A}
ᤋᤱᤘᤠ᤺ᤶᤔᤠ<n>:ᤋᤱ>ᤘᤠ᤺ᤶᤔ{A}
ᤆᤢᤱᤘᤠ᤺ᤶᤔᤠ<n>:ᤆᤢᤱ>ᤘᤠ᤺ᤶᤔ{A}
ᤑᤢᤱᤘᤠ᤺ᤶᤔᤠ<n>:ᤑᤢᤱ>ᤘᤠ᤺ᤶᤔ{A}

# months
ᤁ᤺ᤰᤑᤧᤰᤘᤠ<n>:ᤁ᤺ᤰ>ᤑᤧᤰᤘ{A}
ᤛ᤺ᤵᤑᤧᤰᤘᤠ<n>:ᤛ᤺ᤵ>ᤑᤧᤰᤘ{A}
ᤆᤧᤖᤧᤱᤏᤠᤶ<n>:ᤆᤧ>ᤖᤧᤱᤏᤠ{m}
ᤌᤧᤖᤧᤱᤏᤠᤶ<n>:ᤌᤧ>ᤖᤧᤱᤏᤠ{m}
ᤁᤠᤵᤔᤣ᤺ᤵᤐᤠ<n>:ᤁᤠᤵ>ᤔᤣ᤺ᤵᤐ{A}
ᤌᤰᤔᤣ᤺ᤵᤐᤠ<n>:ᤌᤰ>ᤔᤣ᤺ᤵᤐ{A}
ᤛᤡᤛᤣ᤺ᤰᤐᤠ<n>:ᤛᤡ>ᤛᤣ᤺ᤰᤐ{A}
ᤌᤧᤛᤣ᤺ᤰᤐᤠ<n>:ᤌᤧ>ᤛᤣ᤺ᤰᤐ{A}
ᤛᤡᤛᤣ᤺ᤰᤖᤥ<n>:ᤛᤡ>ᤛᤣ᤺ᤰᤖ{oo}
ᤌᤧᤛᤣ᤺ᤰᤁᤪᤥ<n>:ᤌᤧ>ᤛᤣ᤺ᤰᤁᤪ{oo}
ᤛᤧᤴᤇᤧᤱᤗᤠ<n>:ᤛᤧᤴᤇᤧᤱ>ᤗ{A}
ᤛᤡᤛᤣ᤺ᤰᤗᤠ<n>:ᤛᤡᤛᤣ᤺ᤰ>ᤗ{A}

# math signs
ᤛᤢᤶᤔᤠ<n>:ᤛᤢᤶᤔᤠ{A}
ᤆᤰᤔᤠ<n>:ᤆᤰᤔ{A}
ᤂᤰᤔᤠ<n>:ᤂᤰᤔ{A}
ᤑᤡ᤹<n>:ᤑᤡ᤹
ᤑᤥᤔᤠ<n>:ᤑᤥᤔ{A}
ᤌᤡᤶᤔᤠ<n>:ᤌᤡᤶᤔ{A}
ᤜᤠ᤺ᤔᤠ<n>:ᤜᤠ᤺ᤔ{A}
ᤜᤠ᤺<n>:ᤜ{A}{do}
ᤐᤡᤆᤧᤵ<n>:ᤐᤡᤆᤧ{p}
ᤋᤱᤍᤱ<n>:ᤋᤱᤍ{ng}

# proper Nouns
ᤖᤠ᤺ᤶ<np>:ᤖᤠ᤺{m}
ᤏᤣᤐᤠᤸ<np><top>:ᤏᤣᤐᤠ{ll}
ᤑᤥᤃᤡᤍᤥ<np><top>:ᤑᤥᤃᤡᤍᤥ
ᤁᤖᤍ<np>:ᤁᤖᤍ
ᤌᤧᤈᤱᤒᤥ<np><top>:ᤌᤧᤈᤱᤒ{oo}
ᤌᤱᤗᤱ<np><top>:ᤌᤱᤗ{ng}
ᤗᤡᤱᤎᤧᤵ<np><top>:ᤗᤡᤱᤎᤧ{p}
ᤂᤰᤗᤡᤱ<np><top>:ᤂᤰᤗᤡ{ng}

# biblical proper nouns
ᤋᤣᤖᤜ<np>:ᤋᤣᤖᤜ
ᤀᤒᤪᤠᤔ<np>:ᤀᤒᤪᤠᤔ
ᤏᤠᤜᤥᤖ<np>:ᤏᤠᤜᤥᤖ
ᤜᤠᤖᤠᤏ<np>:ᤜᤠᤖᤠᤏ
ᤗᤢᤳ<np>:ᤗᤢ{t}
ᤀᤢᤖ<np><top>:ᤀᤢᤖ
ᤁᤸᤍᤡᤜᤠ᤹<np>:ᤁᤸᤍᤡᤜᤠ᤹
ᤛᤠᤖᤠᤀᤡ<np>:ᤛᤠᤖᤠᤀᤡ
ᤔᤡᤸᤁᤠ<np>:ᤔᤡᤸᤁᤠ
ᤕᤡᤛ᤻ᤁᤠ<np>:ᤕᤡᤛ᤻ᤁᤠ
ᤁᤏᤠᤏ<np><top>:ᤁᤏᤠᤏ
ᤜᤠᤖᤠᤴ<np>:ᤜᤠᤖᤠ{N}
ᤜᤠᤖᤠᤴ<np>:ᤜᤠᤖᤠ{N}{N}   # Dir/LR
ᤛᤣᤶ<np>:ᤛᤣ{m}
ᤛᤠᤔᤢᤀᤣᤗ<np>:ᤛᤠᤔᤢᤀᤣᤗ
ᤛᤠᤘᤗ<np>:ᤛᤠᤘᤗ
ᤒᤣᤌᤗᤧᤜᤧᤔᤒᤠ<np><m><top>:ᤒᤣᤌᤗᤧᤜᤧᤔ>ᤒ{Aabs}
ᤒᤣᤌᤗᤧᤜᤧᤔ<np><top>:ᤒᤣᤌᤗᤧᤜᤧᤔ
ᤍᤠᤀᤢᤍ<np>:ᤍᤠᤀᤢᤍ
ᤔᤥᤛᤠ<np>:ᤔᤥᤛ{A}
ᤑᤠᤖᤥ<np>:ᤑᤠᤖᤥ
ᤏᤒᤢᤁᤍᤏᤣᤛᤖ<np>:ᤏᤒᤢᤁᤍᤏᤣᤛᤖ
ᤒᤣᤒᤡᤗᤥᤏ<np><top>:ᤒᤣᤒᤡᤗᤥᤏ
ᤗᤠᤶᤗᤥᤒᤠᤜᤠ᤹—ᤕᤖᤢᤒᤠᤒᤣᤗ<np>:ᤗᤠᤶᤗᤥᤒᤠᤜᤠ᤹—ᤕᤖᤢᤒᤠᤒᤣᤗ
ᤕᤣᤛᤢᤀᤠ<np>:ᤕᤣᤛᤢᤀᤠ
ᤏᤜᤧᤔᤩᤠ<np>:ᤏᤜᤧᤔᤩᤠ
ᤛᤖᤠᤕᤠᤜ<np>:ᤛᤖᤠᤕᤠᤜ
ᤖᤣᤗᤕᤠᤜ<np>:ᤖᤣᤗᤕᤠᤜ
ᤔᤥᤷᤍᤁᤤ<np>:ᤔᤥᤷᤍᤁᤤ
ᤒᤡᤸᤛᤠᤏ<np>:ᤒᤡᤸᤛᤠᤏ
ᤔᤡᤛ᤻ᤐᤠᤖ<np>:ᤔᤡᤛ᤻ᤐᤠᤖ
ᤒᤡᤃᤫᤤ<np>:ᤒᤡᤃᤫᤤ
ᤖᤣᤜᤢᤔ<np>:ᤖᤣᤜᤢᤔ
ᤒᤠᤏᤠᤜᤠ<np>:ᤒᤠᤏᤠᤜᤠ
ᤐᤖᤥᤛ<np>:ᤐᤖᤥᤛ
ᤛᤐᤋᤩᤠᤜ<np>:ᤛᤐᤋᤩᤠᤜ
ᤀᤠᤖᤠ<np>:ᤀᤠᤖᤠ
ᤐᤜᤋ-ᤔᤥᤀᤠᤒ<np>:ᤐᤜᤋ-ᤔᤥᤀᤠᤒ
ᤕᤥᤀᤠᤒ<np>:ᤕᤥᤀᤠᤒ
ᤀᤣᤗᤠᤔ<np><top>:ᤀᤣᤗᤠᤔ
ᤈᤳᤋᤢ<np>:ᤈᤳᤋᤢ
ᤈᤰᤁᤤ<np>:ᤈᤰᤁᤤ
ᤒᤠᤏᤡ<np>:ᤒᤠᤏᤡ
ᤒᤣᤒᤤ<np>:ᤒᤣᤒᤤ
ᤀᤈ᤻ᤃᤠᤍ<np>:ᤀᤈ᤻ᤃᤠᤍ
ᤀᤍᤥᤏᤡᤁᤠᤔ<np>:ᤀᤍᤥᤏᤡᤁᤠᤔ
ᤀᤠᤍᤡᤏ<np>:ᤀᤠᤍᤡᤏ
ᤀᤠᤋᤣᤖ<np>:ᤀᤠᤋᤣᤖ
ᤜᤡᤈᤁᤡᤕᤠ<np>:ᤜᤡᤈᤁᤡᤕ{A}
ᤒᤣᤈᤤ<np>:ᤒᤣᤈᤤ
ᤕᤥᤖᤠ<np>:ᤕᤥᤖ{A}
ᤜᤠᤛᤢᤔ<np>:ᤜᤠᤛᤢᤔ
ᤃᤡᤒ᤻ᤒᤠᤖ<np>:ᤃᤡᤒ᤻ᤒᤠᤖ
ᤏᤋᤥᤐᤠ<np>:ᤏᤋᤥᤐ{A}
ᤀᤒᤪᤠᤜᤠᤶ<np>:ᤀᤒᤪᤠᤜᤠ{m}
ᤀᤒᤪᤠᤜᤠᤶ<np>:ᤀᤒᤪᤠᤜᤠ{m}{m}   # Dir/LR
ᤛᤠᤖᤠ<np>:ᤛᤠᤖ{A}
ᤔᤡᤛᤪ<np><top>:ᤔᤡᤛᤪ
ᤔᤡᤛᤪᤡ<np>:ᤔᤡᤛᤪᤡ
ᤀᤠᤍᤶ<np>:ᤀᤠᤍ{m}
ᤀᤠᤍᤶ<np>:ᤀᤠᤍ{m}{m}     # Dir/LR
ᤏᤢᤜ<np>:ᤏᤢᤜ
ᤀᤡᤛ᤻ᤔᤠᤀᤣᤗ<np>:ᤀᤡᤛ᤻ᤔᤠᤀᤣᤗ
ᤀᤡᤛᤪᤠᤀᤡᤗ<np><top>:ᤀᤡᤛᤪᤠᤀᤡᤗ
ᤀᤡᤛᤪᤠᤀᤡᤗᤡ<np>:ᤀᤡᤛᤪᤠᤀᤡᤗᤡ
ᤗᤠᤒᤠᤏ<np>:ᤗᤠᤒᤠ{N}

# bible chapter names
ᤗᤠᤶᤍᤡᤰ<np>:ᤗᤠᤶᤍᤡ{k}
ᤗᤧᤒᤡ<np>:ᤗᤧᤒᤡ
ᤂᤠᤏᤡ᤺ᤳ<np>:ᤂᤠᤏᤡ᤺{t}
ᤛᤠᤶᤕᤥᤌᤡᤶ<np>:ᤛᤠᤶᤕᤥᤌᤡ{m}
ᤀᤡᤱ<np>:ᤀᤡ{ng}
ᤕᤜᤥᤛᤢ<np>:ᤕᤜᤥᤛᤢ
ᤂᤠᤛᤧᤴᤗᤥ᤺ᤶᤒᤠ<np>:ᤂᤠᤛᤧᤴᤗᤥ᤺ᤶ>ᤒ{Aabs}
ᤖᤢᤌ<np>:ᤖᤢᤌ
ᤛᤠᤵᤔᤢ᤺ᤴᤎᤢᤶ<np>:ᤛᤠᤵᤔᤢ᤺ᤴᤎᤢ{m}
ᤀᤣᤈᤪᤠ<np>:ᤀᤣᤈᤪ{A}
ᤏᤜᤣᤔᤩᤠᤜ<np>:ᤏᤜᤣᤔᤩᤠᤜ
ᤀᤣᤛ᤻ᤋᤖ<np>:ᤀᤣᤛ᤻ᤋᤖ
ᤀᤕᤩᤢᤒ<np>:ᤀᤕᤩᤢᤒ
ᤏᤡᤱᤘᤠ᤹ᤑᤢᤛᤠᤶᤗᤥ<np>:ᤏᤡᤱᤘᤠ᤹ᤑᤢᤛᤠᤶᤗᤥ
ᤛᤢᤶ<np>:ᤛᤢ{m}
ᤂᤠᤜᤢᤴ<np>:ᤂᤠᤜᤢ{N}
ᤂᤠᤜᤢᤴᤛᤠᤒᤠ<np><m>:ᤂᤠᤜᤢᤴᤛᤠ>ᤒ{Aabs}
ᤛᤥᤗᤥᤔᤏ<np>:ᤛᤥᤗᤥᤔᤏ
ᤁᤢᤛᤠᤶᤗᤥ<np>:ᤁᤢᤛᤠᤶᤗᤥ
ᤕᤛᤤᤕᤠ<np>:ᤕᤛᤤᤕ{A}
ᤕᤷᤔᤡᤕᤠ<np>:ᤕᤷᤔᤡᤕ{A}
ᤔᤧᤖᤡᤱ<np>:ᤔᤧᤖᤡ{ng}
ᤛᤠᤵᤐᤴ<np>:ᤛᤠᤵᤐ{N}
ᤀᤡᤈᤁᤡᤀᤣᤗ<np>:ᤀᤡᤈᤁᤡᤀᤣᤗ
ᤍᤠᤏᤡᤀᤣᤗ<np>:ᤍᤠᤏᤡᤀᤣᤗ
ᤜᤥᤛᤣ<np>:ᤜᤥᤛᤣ
ᤕᤥᤀᤣᤗ<np>:ᤕᤥᤀᤣᤗ
ᤀᤠᤔᤥᤛ<np>:ᤀᤠᤔᤥᤛ
ᤀᤥᤒᤍᤡᤕᤠ<np>:ᤀᤥᤒᤍᤡᤕ{A}
ᤕᤥᤏᤠ<np>:ᤕᤥᤏ{A}
ᤔᤡᤁᤠ<np>:ᤔᤡᤁ{A}
ᤏᤜᤢᤔ<np>:ᤏᤜᤢᤔ
ᤜᤒᤁᤢᤁ<np>:ᤜᤒᤁᤢᤁ
ᤛᤐᤏᤩᤠᤜ<np>:ᤛᤐᤏᤩᤠᤜ
ᤜᤠᤃ᤻ᤃᤤ<np>:ᤜᤠᤃ᤻ᤃᤤ
ᤈᤁᤖᤡᤕᤠ<np>:ᤈᤁᤖᤡᤕᤠ
ᤔᤗᤠᤁᤡ<np>:ᤔᤗᤠᤁᤡ
ᤔᤳᤋᤡ<np>:ᤔᤳᤋᤡ
ᤔᤷᤁᤢᤛ<np>:ᤔᤷᤁᤢᤛ
ᤗᤢᤁᤠ<np>:ᤗᤢᤁ{A}
ᤕᤢᤜᤢᤴᤏᤠ<np>:ᤕᤢᤜᤢᤴᤏ{A}
ᤛᤣ᤺ᤰᤕᤢᤰᤔᤡᤒᤠ<np>:ᤛᤣ᤺ᤰᤕᤢᤰᤔᤡ>ᤒ{Aabs}
ᤖᤥᤔᤡ<np>:ᤖᤥᤔᤡ
ᤁᤥᤖᤴᤌᤡ<np>:ᤁᤥᤖᤴᤌᤡ
ᤃᤗᤠᤋᤡ<np>:ᤃᤗᤠᤋᤡ
ᤀᤣᤑᤡᤛᤡ<np>:ᤀᤣᤑᤡᤛᤡ
ᤑᤡᤗᤡᤵᤐᤡ<np>:ᤑᤡᤗᤡᤵᤐᤡ
ᤁᤗᤛ᤻ᤛᤡ<np>:ᤁᤗᤛ᤻ᤛᤡ
ᤌᤡᤛ᤻ᤛᤗᤥᤏᤡᤁᤡ<np>:ᤌᤡᤛ᤻ᤛᤗᤥᤏᤡᤁᤡ
ᤋᤡᤔᤥᤌᤡ<np>:ᤋᤡᤔᤥᤌᤡ
ᤋᤡᤋᤛ<np>:ᤋᤡᤋᤛ
ᤑᤡᤗᤣᤔᤥᤏ<np>:ᤑᤡᤗᤣᤔᤥᤏ
ᤜᤡᤒᤪᤢ<np>:ᤜᤡᤒᤪᤢ
ᤕᤠᤁᤢᤒ<np>:ᤕᤠᤁᤢᤒ
ᤐᤋᤪᤢᤛ<np>:ᤐᤋᤪᤢᤛ
ᤕᤜᤢᤍᤠ<np>:ᤕᤜᤢᤍᤠ
ᤛᤢᤑᤥ<np>:ᤛᤢᤑᤥ

# ones with unknown meaning for now
ᤁᤢᤛ᤻ᤛᤠ<n>:ᤁᤢᤛ᤻ᤛᤠ{do}
ᤕᤧᤶᤔᤥ<n>:ᤕᤧᤶᤔᤥ

LEXICON NumeralStems
ᤜᤥᤵ<num>:ᤜᤥ{p}
ᤌᤡᤰ<num>:ᤌᤡ{k}
ᤗᤌᤡᤰ<num>:ᤗ>ᤌᤡ{k}    # Dir/LR
ᤏᤧᤜᤡ<num>:ᤏᤧ>ᤜᤡ
ᤛᤢᤶᤛᤡ<num>:ᤛᤢᤶ>ᤛᤡ
ᤗᤡᤛᤡ<num>:ᤗᤡ>ᤛᤡ
ᤏᤠᤛᤡ<num>:ᤏᤠ>ᤛᤡ
ᤅᤠᤛᤡ<num>:ᤅᤠ>ᤛᤡ       # Dir/LR
ᤋᤢᤰᤛᤡ<num>:ᤋᤢᤰ>ᤛᤡ
ᤏᤢᤛᤡ<num>:ᤏᤢ>ᤛᤡ
ᤕᤧᤜᤡ<num>:ᤕᤧᤳ>ᤜᤡ
ᤑᤠᤱᤛᤡ<num>:ᤑᤠᤱ>ᤛᤡ
ᤒᤥᤱ<num>:ᤒᤥ{ng}
ᤌᤡᤒᤥᤱ<num>:ᤌᤡᤒᤥ{ng}   # Dir/LR
ᤌᤡᤰᤌᤡᤰ<num>:ᤌᤡᤰᤌᤡ{k}
ᤌᤡᤰᤏᤧᤳ<num>:ᤌᤡᤰᤏᤧ{t}
ᤌᤡᤰᤏᤧ᤹ᤸ<num>:ᤌᤡᤰᤏᤧ᤹{l}   # Dir/LR
ᤌᤡᤰᤛᤢᤶ<num>:ᤌᤡᤰᤛᤢ{m}
ᤌᤡᤰᤗᤡ<num>:ᤌᤡᤰᤗᤡ
ᤌᤡᤰᤅᤠ<num>:ᤌᤡᤰᤅ{A}
ᤌᤡᤰᤏᤠ<num>:ᤌᤡᤰᤏ{A}    # Dir/LR
ᤌᤡᤰᤋᤢᤰ<num>:ᤌᤡᤰᤋᤢ{k}
ᤌᤡᤰᤌᤢᤰ<num>:ᤌᤡᤰᤌᤢ{k}   # Dir/LR
ᤌᤡᤰᤏᤢ<num>:ᤌᤡᤰᤏᤢ
ᤌᤡᤰᤕᤧᤳ<num>:ᤌᤡᤰᤕᤧ{t}
ᤌᤡᤰᤕᤧ᤹ᤸ<num>:ᤌᤡᤰᤕᤧ᤹{l}    # Dir/LR
ᤌᤡᤰᤑᤠᤱ<num>:ᤌᤡᤰᤑᤠ{ng}
ᤏᤡᤒᤥ᤺ᤱ<num>:ᤏᤡᤒᤥ{do}{ng}
ᤏᤧᤳᤌᤡᤰ<num>:ᤏᤡᤒᤥ᤺ᤱ+ᤌᤡ{k}
ᤏᤧᤳᤏᤧᤳ<num>:ᤏᤡᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ
ᤏᤧᤳᤏᤧ᤹ᤸ<num>:ᤏᤡᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ     # Dir/LR
ᤏᤧᤳᤆᤢᤶ<num>:ᤏᤡᤒᤥ᤺ᤱ+ᤛᤢᤶᤛᤡ
ᤏᤧᤳᤗᤡ<num>:ᤏᤡᤒᤥ᤺ᤱ+ᤗᤡᤛᤡ
ᤏᤧᤳᤏᤠ<num>:ᤏᤡᤒᤥ᤺ᤱ+ᤏᤠᤛᤡ
ᤏᤧᤳᤋᤢᤰ<num>:ᤏᤡᤒᤥ᤺ᤱ+ᤋᤢᤰᤛᤡ
ᤏᤧᤳᤏᤢ<num>:ᤏᤡᤒᤥ᤺ᤱ+ᤏᤢᤛᤡ
ᤏᤧᤳᤕᤧᤳ<num>:ᤏᤡᤒᤥ᤺ᤱ+ᤕᤧᤜᤡ
ᤏᤧᤳᤑᤠᤱ<num>:ᤏᤡᤒᤥ᤺ᤱ+ᤑᤠᤱᤛᤡ
ᤛᤢᤶᤒᤥ᤺ᤱ<num>:ᤛᤢᤶᤒᤥ{do}{ng}
ᤛᤢᤶᤌᤡᤰ<num>:ᤛᤢᤶᤒᤥ᤺ᤱ+ᤌᤡ{k}
ᤛᤢᤶᤎᤡᤰ<num>:ᤛᤢᤶᤒᤥ᤺ᤱ+ᤌᤡ{k}   # Dir/LR
ᤛᤢᤶᤏᤧᤳ<num>:ᤛᤢᤶᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ
ᤛᤢᤶᤏᤧ᤹ᤸ<num>:ᤛᤢᤶᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ     # Dir/LR
ᤛᤢᤶᤛᤢᤶ<num>:ᤛᤢᤶᤒᤥ᤺ᤱᤒᤥ᤺ᤱ+ᤛᤢᤶᤛᤡ
ᤛᤢᤶᤗᤡ<num>:ᤛᤢᤶᤒᤥ᤺ᤱ+ᤗᤡᤛᤡ
ᤛᤢᤶᤏᤠ<num>:ᤛᤢᤶᤒᤥ᤺ᤱ+ᤏᤠᤛᤡ
ᤛᤢᤶᤋᤢᤰ<num>:ᤛᤢᤶᤒᤥ᤺ᤱ+ᤋᤢᤰᤛᤡ
ᤛᤢᤶᤏᤢ<num>:ᤛᤢᤶᤒᤥ᤺ᤱ+ᤏᤢᤛᤡ
ᤛᤢᤶᤕᤧᤳ<num>:ᤛᤢᤶᤒᤥ᤺ᤱ+ᤕᤧᤜᤡ
ᤛᤢᤶᤑᤠᤱ<num>:ᤛᤢᤶᤒᤥ᤺ᤱ+ᤑᤠᤱᤛᤡ
ᤗᤡᤒᤥ᤺ᤱ<num>:ᤗᤡᤒᤥ{do}{ng}
ᤗᤡᤁᤡᤵ<num>:ᤗᤡᤁᤡ{p}
ᤗᤡᤌᤡᤰ<num>:ᤗᤡᤒᤥ᤺ᤱ+ᤌᤡ{k}
ᤗᤡᤏᤧᤳ<num>:ᤗᤡᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ
ᤗᤡᤏᤧ᤹ᤸ<num>:ᤗᤡᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ     # Dir/LR
ᤗᤡᤛᤢᤶ<num>:ᤗᤡᤒᤥ᤺ᤱ+ᤛᤢᤶᤛᤡ
ᤗᤡᤗᤡ<num>:ᤗᤡᤒᤥ᤺ᤱ+ᤗᤡᤛᤡ
ᤗᤡᤏᤠ<num>:ᤗᤡᤒᤥ᤺ᤱ+ᤏᤠᤛᤡ
ᤗᤡᤋᤢᤰ<num>:ᤗᤡᤒᤥ᤺ᤱ+ᤋᤢᤰᤛᤡ
ᤗᤡᤏᤢ<num>:ᤗᤡᤒᤥ᤺ᤱ+ᤏᤢᤛᤡ
ᤗᤡᤕᤧᤳ<num>:ᤗᤡᤒᤥ᤺ᤱ+ᤕᤧᤜᤡ
ᤗᤡᤑᤠᤱ<num>:ᤗᤡᤒᤥ᤺ᤱ+ᤑᤠᤱᤛᤡ
ᤅᤠᤒᤥ᤺ᤱ<num>:ᤅᤠᤒᤥ{do}{ng}
ᤏᤠᤁᤡᤵ<num>:ᤏᤠᤁᤡᤵ         # Dir/LR
ᤏᤠᤌᤡᤰ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤌᤡ{k}
ᤅᤠᤌᤡᤰ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤌᤡ{k}  # Dir/LR
ᤏᤠᤏᤧᤳ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ
ᤅᤠᤏᤧᤳ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ    # Dir/LR
ᤏᤠᤏᤧ᤹ᤸ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ     # Dir/LR
ᤅᤠᤏᤧ᤹ᤸ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ    # Dir/LR
ᤏᤠᤛᤢᤶ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤛᤢᤶᤛᤡ
ᤅᤠᤛᤢᤶ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤛᤢᤶᤛᤡ   # Dir/LR
ᤏᤠᤗᤡ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤗᤡᤛᤡ
ᤅᤠᤗᤡ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤗᤡᤛᤡ     # Dir/LR
ᤏᤠᤏᤠ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤏᤠᤛᤡ
ᤅᤠᤏᤠ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤏᤠᤛᤡ     # Dir/LR
ᤏᤠᤋᤢᤰ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤋᤢᤰᤛᤡ
ᤅᤠᤋᤢᤰ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤋᤢᤰᤛᤡ   # Dir/LR
ᤏᤠᤏᤢ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤏᤢᤛᤡ
ᤅᤠᤏᤢ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤏᤢᤛᤡ     # Dir/LR
ᤏᤠᤕᤧᤳ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤕᤧᤜᤡ
ᤅᤠᤕᤧᤳ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤕᤧᤜᤡ    # Dir/LR
ᤏᤠᤑᤠᤱ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤑᤠᤱᤛᤡ
ᤅᤠᤑᤠᤱ<num>:ᤅᤠᤒᤥ᤺ᤱ+ᤑᤠᤱᤛᤡ   # Dir/LR
ᤋᤢᤰᤒᤥ᤺ᤱ<num>:ᤋᤢᤰᤒᤥ{do}{ng}
ᤌᤢᤰᤁᤡᤵ<num>:ᤌᤢᤰᤁᤡᤵ       # Dir/LR
ᤌᤢᤰᤌᤡᤰ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤌᤡ{k}
ᤋᤢᤰᤌᤡᤰ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤌᤡ{k}  # Dir/LR
ᤌᤢᤰᤏᤧᤳ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ
ᤋᤢᤰᤏᤧᤳ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ    # Dir/LR
ᤌᤢᤰᤏᤧ᤹ᤸ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ     # Dir/LR
ᤋᤢᤰᤏᤧ᤹ᤸ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ    # Dir/LR
ᤌᤢᤰᤛᤢᤶ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤛᤢᤶᤛᤡ
ᤋᤢᤰᤛᤢᤶ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤛᤢᤶᤛᤡ   # Dir/LR
ᤌᤢᤰᤗᤡ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤗᤡᤛᤡ
ᤋᤢᤰᤗᤡ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤗᤡᤛᤡ     # Dir/LR
ᤌᤢᤰᤏᤠ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤏᤠᤛᤡ
ᤋᤢᤰᤏᤠ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤏᤠᤛᤡ     # Dir/LR
ᤌᤢᤰᤌᤢᤰ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤋᤢᤰᤛᤡ
ᤋᤢᤰᤋᤢᤰ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤋᤢᤰᤛᤡ   # Dir/LR
ᤌᤢᤰᤏᤢ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤏᤢᤛᤡ
ᤋᤢᤰᤏᤢ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤏᤢᤛᤡ     # Dir/LR
ᤌᤢᤰᤕᤧᤳ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤕᤧᤜᤡ
ᤋᤢᤰᤕᤧᤳ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤕᤧᤜᤡ    # Dir/LR
ᤌᤢᤰᤑᤠᤱ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤑᤠᤱᤛᤡ
ᤋᤢᤰᤑᤠᤱ<num>:ᤋᤢᤰᤒᤥ᤺ᤱ+ᤑᤠᤱᤛᤡ   # Dir/LR
ᤏᤢᤒᤥ᤺ᤱ<num>:ᤏᤢᤒᤥ{do}{ng}
ᤏᤢᤁᤡᤵ<num>:ᤏᤢᤁᤡ{p}
ᤏᤢᤌᤡᤰ<num>:ᤏᤢᤒᤥ᤺ᤱ+ᤌᤡ{k}
ᤏᤢᤏᤧᤳ<num>:ᤏᤢᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ
ᤏᤢᤏᤧ᤹ᤸ<num>:ᤏᤢᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ     # Dir/LR
ᤏᤢᤛᤢᤶ<num>:ᤏᤢᤒᤥ᤺ᤱ+ᤛᤢᤶᤛᤡ
ᤏᤢᤗᤡ<num>:ᤏᤢᤒᤥ᤺ᤱ+ᤗᤡᤛᤡ
ᤏᤢᤏᤠ<num>:ᤏᤢᤒᤥ᤺ᤱ+ᤏᤠᤛᤡ
ᤏᤢᤋᤢᤰ<num>:ᤏᤢᤒᤥ᤺ᤱ+ᤋᤢᤰᤛᤡ
ᤏᤢᤏᤢ<num>:ᤏᤢᤒᤥ᤺ᤱ+ᤏᤢᤛᤡ
ᤏᤢᤕᤧᤳ<num>:ᤏᤢᤒᤥ᤺ᤱ+ᤕᤧᤜᤡ
ᤏᤢᤑᤠᤱ<num>:ᤏᤢᤒᤥ᤺ᤱ+ᤑᤠᤱᤛᤡ
ᤕᤧᤳᤒᤥ᤺ᤱ<num>:ᤕᤧᤳᤒᤥ{do}{ng}
ᤕᤧ᤺ᤁᤡᤵ<num>:ᤕᤧ᤺ᤁᤡ{p}      # Dir/LR
ᤕᤧᤳᤌᤡᤰ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤌᤡ{k}
ᤕᤧ᤺ᤌᤡᤰ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤌᤡ{k}  # Dir/LR
ᤕᤧᤳᤏᤧᤳ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ
ᤕᤧ᤺ᤏᤧᤳ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ    # Dir/LR
ᤕᤧᤳᤏᤧ᤹ᤸ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ     # Dir/LR
ᤕᤧ᤺ᤏᤧ᤹ᤸ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ    # Dir/LR
ᤕᤧᤳᤛᤢᤶ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤛᤢᤶᤛᤡ
ᤕᤧ᤺ᤛᤢᤶ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤛᤢᤶᤛᤡ   # Dir/LR
ᤕᤧᤳᤗᤡ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤗᤡᤛᤡ
ᤕᤧ᤺ᤗᤡ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤗᤡᤛᤡ     # Dir/LR
ᤕᤧᤳᤏᤠ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤏᤠᤛᤡ
ᤕᤧ᤺ᤏᤠ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤏᤠᤛᤡ     # Dir/LR
ᤕᤧᤳᤌᤢᤰ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤋᤢᤰᤛᤡ
ᤕᤧ᤺ᤋᤢᤰ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤋᤢᤰᤛᤡ   # Dir/LR
ᤕᤧᤳᤏᤢ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤏᤢᤛᤡ
ᤕᤧ᤺ᤏᤢ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤏᤢᤛᤡ     # Dir/LR
ᤕᤧᤳᤕᤧᤳ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤕᤧᤜᤡ
ᤕᤧ᤺ᤕᤧᤳ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤕᤧᤜᤡ    # Dir/LR
ᤕᤧᤳᤑᤠᤱ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤑᤠᤱᤛᤡ
ᤕᤧ᤺ᤑᤠᤱ<num>:ᤕᤧᤳᤒᤥ᤺ᤱ+ᤑᤠᤱᤛᤡ   # Dir/LR
ᤑᤠᤱᤒᤥ᤺ᤱ<num>:ᤑᤠᤱᤒᤥ{do}{ng}
ᤑᤠᤱᤃᤡᤵ<num>:ᤑᤠᤱᤃᤡ{p}
ᤑᤠᤱᤎᤡᤰ<num>:ᤑᤠᤱᤒᤥ᤺ᤱ+ᤌᤡ{k}
ᤑᤠᤱᤏᤧᤳ<num>:ᤑᤠᤱᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ
ᤑᤠᤱᤏᤧ᤹ᤸ<num>:ᤑᤠᤱᤒᤥ᤺ᤱ+ᤏᤧᤜᤡ     # Dir/LR
ᤑᤠᤱᤛᤢᤶ<num>:ᤑᤠᤱᤒᤥ᤺ᤱ+ᤛᤢᤶᤛᤡ
ᤑᤠᤱᤗᤡ<num>:ᤑᤠᤱᤒᤥ᤺ᤱ+ᤗᤡᤛᤡ
ᤑᤠᤱᤏᤠ<num>:ᤑᤠᤱᤒᤥ᤺ᤱ+ᤏᤠᤛᤡ
ᤑᤠᤱᤋᤢᤰ<num>:ᤑᤠᤱᤒᤥ᤺ᤱ+ᤋᤢᤰᤛᤡ
ᤑᤠᤱᤏᤢ<num>:ᤑᤠᤱᤒᤥ᤺ᤱ+ᤏᤢᤛᤡ
ᤑᤠᤱᤕᤧᤳ<num>:ᤑᤠᤱᤒᤥ᤺ᤱ+ᤕᤧᤜᤡ
ᤑᤠᤱᤑᤠᤱ<num>:ᤑᤠᤱᤒᤥ᤺ᤱ+ᤑᤠᤱᤛᤡ
ᤌᤡᤰᤁᤡᤵ<num>:ᤌᤡᤰᤁᤡ{p}     # Dir/LR
ᤁᤡᤵᤌᤡᤰ<num>:ᤁᤡᤵᤌᤡ{k}
ᤁᤡᤵ<num>:ᤁᤡ{p}

LEXICON Units
ᤌᤡᤰ:ᤌᤡ{k}
ᤁᤠᤵᤛᤡᤱ:ᤁᤠᤵᤛᤡ{ng}
ᤏᤠᤴᤇᤡᤱ:ᤏᤠᤴᤇᤡ{ng}
ᤜᤣᤴᤇᤡᤱ:ᤜᤣᤴᤇᤡ{ng}
ᤌᤡᤒᤥᤱᤜᤣᤴᤇᤡᤱ:ᤌᤡᤒᤥᤱᤜᤣᤴᤇᤡ{ng}
ᤗᤠᤅᤧᤴ:ᤗᤠᤅᤧ{N}
ᤌᤡᤒᤥᤱᤗᤠᤅᤧᤴ:ᤌᤡᤒᤥᤱᤗᤠᤅᤧ{N}
ᤋᤥᤛᤡᤱ:ᤋᤥᤛᤡ{ng}

LEXICON Ordinals
ᤌᤡᤰᤛᤡᤃᤧᤰ:ᤌᤡᤰ>ᤛᤡ>ᤃᤧ{k}
ᤏᤡᤛᤡᤃᤧᤰ:ᤏᤧ>ᤛᤡ>ᤃᤧ{k}
ᤛᤢᤶᤛᤡᤃᤧᤰ:ᤛᤢᤶ>ᤛᤡ>ᤃᤧ{k}
ᤗᤡᤛᤡᤃᤧᤰ:ᤗᤡ>ᤛᤡ>ᤃᤧ{k}
ᤏᤠᤛᤡᤃᤧᤰ:ᤏᤠ>ᤛᤡ>ᤃᤧ{k}
ᤅᤠᤛᤡᤃᤧᤰ:ᤅᤠ>ᤛᤡ>ᤃᤧ{k}      # Dir/LR
ᤋᤢᤰᤛᤡᤃᤧᤰ:ᤋᤢᤰ>ᤛᤡ>ᤃᤧ{k}
ᤏᤢᤛᤡᤃᤧᤰ:ᤏᤢ>ᤛᤡ>ᤃᤧ{k}
ᤕᤧᤳᤛᤡᤃᤧᤰ:ᤕᤧᤳ>ᤛᤡ>ᤃᤧ{k}
ᤑᤠᤱᤛᤡᤃᤧᤰ:ᤑᤠᤱ>ᤛᤡ>ᤃᤧ{k}
ᤌᤡᤒᤥᤱᤛᤡᤃᤧᤰ:ᤌᤡᤒᤥᤱ>ᤛᤡ>ᤃᤧ{k}   # Dir/LR
ᤌᤡᤰᤗᤧᤱ:ᤌᤡᤰ>ᤗᤧ{ng}
ᤏᤡᤖᤧᤱ:ᤏᤧ>ᤖᤧ{ng}
ᤛᤢᤶᤗᤧᤱ:ᤛᤢᤶ>ᤗᤧ{ng}
ᤗᤡᤗᤧᤱ:ᤗᤡ>ᤗᤧ{ng}
ᤏᤠᤖᤧᤱ:ᤏᤠ>ᤖᤧ{ng}
ᤅᤠᤖᤧᤱ:ᤅᤠ>ᤖᤧ{ng}      # Dir/LR
ᤋᤢᤰᤗᤧᤱ:ᤋᤢᤰ>ᤗᤧ{ng}
ᤏᤢᤗᤧᤱ:ᤏᤢ>ᤗᤧ{ng}
ᤕᤧᤳᤗᤧᤱ:ᤕᤧᤳ>ᤗᤧ{ng}
ᤑᤠᤱᤗᤧᤱ:ᤑᤠᤱ>ᤗᤧ{ng}
ᤌᤡᤒᤥᤱᤗᤧᤱ:ᤌᤡᤒᤥᤱ>ᤗᤧ{ng}   # Dir/LR

PATTERN Number
Numeral [<num>:]

LEXICON Numeral
/([0123456789])+/
/([0123456789])+[.,]([0123456789])+/

/([᥆᥇᥈᥉᥊᥋᥌᥍᥎᥏])+/
/([᥆᥇᥈᥉᥊᥋᥌᥍᥎᥏])+[.,]([᥆᥇᥈᥉᥊᥋᥌᥍᥎᥏])+/

LEXICON Punctuation

᥅<sent>:᥅
॥<sent>:॥
᥄<sent>:᥄
।<sent>:।
᥀<sent>:᥀
.<cm>:.

.<cm>:.
..<sent>:..
...<sent>:...
;<sent>:;
\:<sent>:\:
!<sent>:!
-<guio>:-
—<guio>:—
,<cm>:,
?<sent>:? 
'<apos>:'
"<quot>:"
«<lquot>:«
»<rquot>:»
”<rquot>:”
“<lquot>:“
(<lpar>:(
\]<rpar>:\]
\[<lpar>:\[
)<rpar>:)
\\<sent>:\\
\/<sent>:\/


# Resources:
# https://github.com/apertium/lexd/blob/master/Usage.md
# https://wiki.apertium.org/wiki/List_of_symbols
# https://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
# https://wiki.apertium.org/wiki/Lttoolbox_and_lexc
# https://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
# https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
# https://github.com/hfst/
# https://wiki.apertium.org/wiki/Hfst

