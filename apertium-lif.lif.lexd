# Morphological Transducer for Limbu

# Part of speech categories
# <n>      # Noun
# <v>      # Verb
# <adj>    # Adjective
# <num>    # Numeral

# <sent>   # Sentence-ending punctuation
# <guio>   # Hyphen
# <cm>     # Comma
# <apos>   # Apostrophe
# <quot>   # Quotation mark
# <lquot>  # Left quotation mark
# <rquot>  # Right quotation mark
# <lpar>   # Left parenthesis
# <rpar>   # Right parenthesis

# Number morphology
# <pl>     # Plural
# <sg>     # Singular
# <du>     # Dual

# Other symbols
# >        # Morpheme boundary

###############################################################################
###                       M O R P H O T A C T I C S                         ###
###############################################################################

PATTERNS
Number
Punctuation
Nouns
Verbs
Adjectives
Determiners-Words
Conjunctions [ <cnjcoo>: ]
Adverbs
Question-Particles
CaseMarker
Pronoun-Words
Conjoints
Prepositions [ <pr>: ]
Complementizer [ <comp>: ]
Concession [ <concess>: ]
Interjection

PATTERN Interjection
Ij-Stem [ <ij>: ] ( [ +:> ]Question-Particles)?

PATTERN Conjoints
(Pronouns [ +:> ] )? Conjoint-Stem [ <cj>: ]

PATTERN Adverbs
(Pronouns [ +:> ] )? AdverbStem [ <adv>: ] ( [ +:> ] Conjoints)?

PATTERN Adjectives
Adjective-Stem [ <adj>: ] Adj-Num

LEXICON Adj-Num
<sg>:
<pl>:>ᤄᤠ
<pl>:>ᤜᤠ᤹ # Dir/LR
<du>:>ᤄᤠᤇᤡ
<du>:>ᤜᤠ᤹ᤇᤡ # Dir/LR

PATTERN Determiners-Words
(Adjectives [ +:> ] )? Determiners [ <det>: ]

PATTERN Pronoun-Words
Pronouns Adverbs?
DemPrn Plurals? Cases?

PATTERN Nouns
(Adjectives [ +:> ] )? PossessivePrn?(1) Noun-Stems [<n>:] Plurals PossessivePrn?(2) Cases ( [ +:> ] CaseMarker)?

PATTERN Verbs
( (Adjectives [ +:> ] )? SubjPersonNumber(1) SubjPersonNumber(4) Negation?(1) Verb-Stems GerundMarker? Negation?(2) Reflexivity&Port? Tense SubjPersonNumber(5) SubjPersonNumber(2) ObjSlot SubjPersonNumber(6) NegationSuff? Aspect? SubjPersonNumber(7) ObjNumber SubjPersonNumber(8) SubjPersonNumber(3) NegationSuff2? ( [ +:> ] CaseMarker)? ( [ +:> ] Adverbs)? ( [ +:> ] Conjoints)? ( [ +:> ] Concession)? )[^[past,nonpast],^[ssg1,ssg23],^[noport,bigport]]
( (Adjectives [ +:> ] )? SubjPersonNumber(1) SubjPersonNumber(4)? Negation?(1) Verb-Stems GerundMarker? Negation?(2) Reflexivity&Port? Tense SubjPersonNumber(5)? SubjPersonNumber(2) ObjSlot SubjPersonNumber(6)? NegationSuff? Aspect? SubjPersonNumber(7)? ObjNumber SubjPersonNumber(8)? SubjPersonNumber(3) NegationSuff2? ( [ +:> ] CaseMarker)? ( [ +:> ] Adverbs)? ( [ +:> ] Conjoints)? ( [ +:> ] Concession)? )[^[past,nonpast],^[ssg1,ssg23],^[noport,bigport]]  # Dir/LR

LEXICON PossessivePrn(2)
:ᤀᤠ>               <px1sg>:
:ᤀᤠᤴᤆᤡ>         <px1duincl>:
:ᤀᤠᤴᤆᤡᤅᤠ>       <px1duexcl>:
:ᤀᤠᤏᤡ>          <px1plincl>:
:ᤀᤠᤏᤡᤅᤠ>        <px1plexcl>:

:ᤁᤠ>              <px2sg>:
:ᤂᤧᤴᤆᤡ>            <px2du>:
:ᤂᤧᤏᤡ>             <px2pl>:
:ᤁᤢ>              <px3sg>:
:ᤂᤢᤴᤆᤡ>            <px3du>:
:ᤂᤢᤴᤆᤡ>            <px3pl>:

LEXICON SubjPersonNumber(8)
:ᤀᤠ>[ssg1,noport]           <s_sg1>:[ssg1,noport]           <incl>:[ssg1,noport]            :[ssg1,noport]                :[ssg1,noport]           :>{ng}[ssg1,noport]         :[ssg1,noport]              :>{ng}[ssg1,noport]
:ᤀᤠ>[ssg1,noport]           <s_sg1>:[ssg1,noport]           <excl>:>ᤃᤧ[ssg1,noport]          :[ssg1,noport]                :[ssg1,noport]           :>{ng}[ssg1,noport]         :[ssg1,noport]              :>{ng}[ssg1,noport]
:ᤀᤠ>[ssg23,noport]          <s_du1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤛ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]                 # Dir/LR
:ᤀᤠ>[ssg23,noport]          <s_du1>:[ssg23,noport]          <excl>:>ᤃᤧ[ssg23,noport]         :ᤔ{E}>[ssg23,noport]          :>ᤛ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]                 # Dir/LR
:ᤀᤠ>[ssg23,noport]          <s_du1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤇ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]
:ᤀᤠ>[ssg23,noport]          <s_du1>:[ssg23,noport]          <excl>:>ᤃᤧ[ssg23,noport]         :ᤔ{E}>[ssg23,noport]          :>ᤇ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]
:[ssg23,noport]            <s_du1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤛ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]                 # Dir/LR
:[ssg23,noport]            <s_du1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤇ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]
:ᤀᤠ>[ssg23,noport]          <s_pl1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:ᤀᤠ>[ssg23,noport]          <s_pl1>:[ssg23,noport]          <excl>:>ᤃᤧ[ssg23,noport]         :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:[ssg23,noport]            <s_pl1>:[ssg23,noport]          <excl>:>ᤃᤧ[ssg23,noport]         :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:ᤀᤠ>[ssg23,bigport]         <s_pl1>:[ssg23,bigport]         <excl>:>[ssg23,bigport]         :ᤔ{E}>[ssg23,bigport]         :[ssg23,bigport]         :[ssg23,bigport]            :>{m}[ssg23,bigport]        :>{m}[ssg23,bigport]             # Dir/LR
:ᤀᤠ>[past,ssg23,noport]     <s_pl1>:[past,ssg23,noport]     <excl>:>ᤃᤧ[past,ssg23,noport]    :ᤔ{E}>[past,ssg23,noport]     :[past,ssg23,noport]     :[past,ssg23,noport]        :>ᤔᤏᤠ[past,ssg23,noport]     :[past,ssg23,noport]
:ᤀᤠ>[past,ssg23,bigport]    <s_pl1>:[past,ssg23,bigport]    <excl>:>[past,ssg23,bigport]    :ᤔ{E}>[past,ssg23,bigport]    :[past,ssg23,bigport]    :[past,ssg23,bigport]       :>ᤔᤏᤠ[past,ssg23,bigport]    :[past,ssg23,bigport]            # Dir/LR
:[ssg23,noport]            <s_pl1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:{K}{E}>[ssg23,noport]     <s_sg2>:[ssg23,noport]                :[ssg23,noport]           :[ssg23,noport]                :[ssg23,noport]         :[ssg23,noport]             :[ssg23,noport]              :[ssg23,noport]
:{K}{E}>[ssg23,noport]     <s_du2>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤛ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]                  # Dir/LR
:{K}{E}>[ssg23,noport]     <s_du2>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤇ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]
:{K}{E}>[ssg23,noport]     <s_pl2>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:[ssg23,noport]            <s_pl2>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:[ssg23,noport]            <s_sg3>:[ssg23,noport]                :[ssg23,noport]           :[ssg23,noport]               :[ssg23,noport]          :[ssg23,noport]             :[ssg23,noport]             :[ssg23,noport]
:[ssg23,noport]            <s_du3>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤛ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]                  # Dir/LR
:[ssg23,noport]            <s_du3>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤇ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]
:[ssg23,noport]            <s_pl3>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]

LEXICON Negation(2)
:ᤔᤧ>       <neg>:       # Dir/LR
:ᤔᤧ{N}>    <neg>:
:ᤏ>       <neg>:

LEXICON GerundMarker
<ger>:>ᤔᤢᤏᤠ

LEXICON Reflexivity&Port
<refl>:>ᤛᤡ{ng}
<refl>:>ᤏᤧ              # Dir/LR
<o_2>:>ᤏᤧ

LEXICON Tense
<nonpast>:[nonpast]
<past>:>{E}[past]

LEXICON ObjSlot
<o_3>:>{a}{u}[noport]
<o_3>:>{u}[noport]       # Dir/LR
<os_pl>:>{a}{i}[noport]
<os_pl>:>{i}[noport]     # Dir/LR
<os_du>:>ᤛᤡ[noport]       # Dir/LR
<os_du>:>ᤇᤡ[noport]
<os_sg>:[noport]

<os_sg12>:>ᤧ[nonpast,noport]
<o_3>:>ᤧ[nonpast,ssg1,noport]
<os_sg12>:>{a}{A}{ng}[past,noport]
<os_sg12>:>{A}{ng}[past,noport]       # Dir/LR
<o_3>:>ᤐᤠ{ng}[past,ssg1,noport]
<​o_3>:>{a}{u}{m}ᤛᤡᤶᤒᤧ[bigport]
<​o_3>:>{u}{m}ᤛᤡᤶᤒᤧ[bigport]            # Dir/LR

LEXICON NegationSuff
<neg>:>{N}

LEXICON Aspect
<prog>:>ᤖ

LEXICON ObjNumber
<o_nonsg>:>ᤛᤡ[noport]
<o_nonsg>:[bigport]      # Dir/LR
<o_sg>:[noport]

LEXICON NegationSuff2
<neg>:>{N}
<neg>:>ᤏᤧ{N} # Dir/LR

LEXICON Plurals
<sg>:
<pl>:>ᤄᤠ
<pl>:>ᤜᤠ᤹ # Dir/LR
<du>:>ᤄᤠᤇᤡ
<du>:>ᤜᤠ᤹ᤇᤡ # Dir/LR

LEXICON Cases
<nom>:
<gen>:>{NG}{A}{n}
<gen>:>ᤖᤧ # Dir/LR
<voc>:>{aa}{o}
<voc>:>{ana}{e} # Dir/LR
<com>:>ᤏᤢ{ng}
<abl>:>ᤗᤠᤠ{m}
<abl>:>ᤏᤢ{ng} # Dir/LR
<erg>:>ᤖᤧ


###############################################################################
###                          L E X I C O N                                  ###
###############################################################################

LEXICON Pronouns
ᤀᤠ<prn><pers><p1><sg>:ᤀᤠ
ᤀᤠ<prn><pers><p1><du><incl>:ᤀᤠᤴᤆᤡ
ᤀᤠ<prn><pers><p1><du><excl>:ᤀᤠᤴᤆᤡᤅᤠ
ᤀᤠ<prn><pers><p1><pl><incl>:ᤀᤠᤏᤡ
ᤀᤠ<prn><pers><p1><pl><excl>:ᤀᤠᤏᤡ>ᤃᤧ # Dir/LR
ᤀᤠ<prn><pers><p1><pl><excl>:ᤀᤠᤏᤡᤅᤠ

# extras for p1
ᤀᤠ<prn><pers><p1><sg>:ᤀᤡᤱᤃᤠ

ᤁᤠ<prn><pers><p2><sg>:ᤁᤠ
ᤂᤧᤴᤆᤡ<prn><pers><p2><du>:ᤂᤧᤴᤆᤡ
ᤂᤧᤏᤡ<prn><pers><p2><pl>:ᤂᤧᤏᤡ
ᤁᤢ<prn><pers><p3><sg>:ᤁᤢ
ᤂᤢᤴᤆᤡ<prn><pers><p3><du>:ᤂᤢᤴᤆᤡ
ᤂᤢᤴᤆᤡ<prn><pers><p3><pl>:ᤂᤢᤴᤆᤡ

# extras (dialectical differences and short forms 
# along with general prefixes)
ᤂᤧᤏᤧ<prn><pers><p2><sg>:ᤂᤧᤏᤧ

LEXICON DemPrn
ᤂᤴ<prn><pers><dem>:ᤂ{N}

LEXICON Prepositions
ᤌᤥ:ᤌᤥ
ᤏᤠ:ᤏᤠ
ᤀᤧᤶᤓᤧᤍᤠᤱᤒᤠ:ᤀᤧᤶᤓᤧᤍᤠᤱᤒᤠ

LEXICON Complementizer
ᤑᤧᤸᤗᤧ:ᤑᤧᤸᤗᤧ

LEXICON Conjoint-Stem
ᤀᤠᤱ:ᤀᤠ{ng}

LEXICON Adjective-Stem
ᤏᤢᤒᤠ:ᤏᤢᤒᤠ
ᤏᤢ:ᤏᤢ
ᤆᤡ:ᤆᤡ
ᤀᤠᤰᤂᤣ:ᤀᤠᤰᤂ{eg}
ᤀ᤹ᤍ᤺ᤱ:ᤀ᤹ᤍ᤺{ng}

LEXICON CaseMarker
# lle Cases
ᤀᤡᤸᤗᤧ<sub>:{a}{i}{l}ᤗᤧ
ᤀᤡᤸᤗᤧ<gen>:{a}{i}{l}ᤗᤧ
ᤀᤡᤸᤗᤧ<erg>:{a}{i}{l}ᤗᤧ
ᤀᤧᤸᤗᤧ<erg>:{a}{E}{l}ᤗᤧ
ᤀᤧᤸᤗᤧ<sub>:{a}{E}{l}ᤗᤧ
ᤀᤧᤸᤗᤧ<gen>:{a}{E}{l}ᤗᤧ

# lle without a
ᤀᤡᤸᤗᤧ<sub>:{i}{l}ᤗᤧ      # Dir/LR
ᤀᤡᤸᤗᤧ<gen>:{i}{l}ᤗᤧ      # Dir/LR
ᤀᤡᤸᤗᤧ<erg>:{i}{l}ᤗᤧ      # Dir/LR
ᤀᤧᤸᤗᤧ<erg>:{E}{l}ᤗᤧ      # Dir/LR
ᤀᤧᤸᤗᤧ<sub>:{E}{l}ᤗᤧ      # Dir/LR
ᤀᤧᤸᤗᤧ<gen>:{E}{l}ᤗᤧ      # Dir/LR

# others
ᤒᤠ<nom>:ᤒᤠ
ᤋᤠᤵ<nom>:ᤋᤠ{p}
ᤛᤧᤴ<nom>:ᤛᤧ{N}
ᤖᤧ<erg>:ᤖᤧ
ᤗᤧ<erg>:ᤗᤧ        # Dir/LR

LEXICON Concession
ᤛᤠᤱ:ᤛᤠ{ng}

LEXICON Question-Particles
ᤀᤡ<qst>:{a}{i}
ᤀᤡ<qst>:{i}      # Dir/LR

LEXICON Determiners
ᤁᤧᤖᤧᤰ:ᤁᤧᤖᤧ{k}
ᤕᤸᤗᤡᤰ:ᤕᤸᤗᤡ{k}
ᤈᤶᤔ:ᤈᤶᤔ
ᤂ:ᤂ
ᤀᤖᤢ:ᤀᤖᤢ

LEXICON Ij-Stem
ᤛᤣᤘᤠᤖᤥ:ᤛᤣᤘᤠᤖᤥ
ᤔᤧ᤺ᤴ:ᤔᤧ᤺{N}

LEXICON Conjunctions
ᤋᤖ:ᤋᤖ

LEXICON AdverbStem
ᤂᤶᤓ:ᤂᤶᤓ
ᤂᤶᤓᤧ:ᤂᤶᤓᤧ           # Dir/LR
ᤀᤧᤶᤓᤧᤸᤗᤧ:ᤀᤧᤶᤓᤧᤸᤗᤧ
ᤕᤠᤶᤔᤢ:ᤕᤠᤶᤔᤢ
ᤖᤳ:ᤖ{t}
ᤀᤠᤱ:{a}{A}{ng}
ᤀᤠᤱ:{A}{ng}         # Dir/LR
ᤀᤠᤓᤣᤍᤠᤱᤒᤠ:ᤀᤠᤓᤣᤍᤠᤱᤒᤠ
ᤀᤠᤸᤗᤠ:ᤀᤠᤸᤗᤠ
ᤔᤧ᤺ᤴᤏ:ᤔᤧ᤺ᤴᤏ
ᤔᤧᤏ᤻ᤏ:ᤔᤧᤏ᤻ᤏ # Dir/LR
ᤌᤧᤕᤠ᤺ᤴᤍᤰ:ᤌᤧᤕᤠ᤺ᤴᤍ{k}
ᤌᤧᤕᤠᤏ᤻ᤍᤰ:ᤌᤧᤕᤠᤏ᤻ᤍ{k}     # Dir/LR
ᤀᤄᤡ:ᤀᤄᤡ
ᤂᤶᤓᤧᤸᤗᤧ:ᤂᤶᤓᤧᤸᤗᤧ
ᤀᤠᤓᤣ:ᤀᤠᤓᤣ
ᤏᤢᤖᤡᤰ:ᤏᤢᤖᤡ{k}

LEXICON Verb-Stems
ᤀᤡᤶᤔᤠ<v>:ᤀᤡ{P}{S}
ᤗᤡᤶᤔᤠ<v>:ᤗᤡᤶ{D}
ᤛᤥ᤺ᤔᤠ<v>:ᤛᤥ᤺{S}
ᤖᤠ᤺ᤰᤔᤠ<v>:ᤖᤠ᤺ᤰ{S}
ᤖᤠᤁ᤻ᤔᤠ<v>:ᤖᤠᤁ᤻{S} # Dir/LR
ᤗᤠ᤺ᤰᤔᤠ<v>:ᤗᤠ᤺ᤰ{S} # Dir/LR
ᤗᤠᤁ᤻ᤔᤠ<v>:ᤗᤠᤁ᤻{S} # Dir/LR
ᤐᤣ᤺ᤰᤔᤠ<v>:{B}{e}{do}{K}
ᤐᤣᤁ᤻ᤔᤠ<v>:{B}{e}{K}{km} # Dir/LR
ᤋᤠ᤺ᤔᤠ<v>:{T}{do}
ᤏᤡᤔᤠ<v>:ᤏᤡ{S}
ᤘᤠᤔᤠ<v>:ᤘᤠ{gs}
ᤛᤧᤶᤔᤠ<v>:ᤛᤧ{do}{R}
ᤆᤥ᤺ᤰᤔᤠ<vbdo>:{ch}{oo}{K}
ᤆᤥᤁ᤻ᤔᤠ<vbdo>:{ch}{K}{km} # Dir/LR
ᤆᤠᤔᤠ<v>:ᤆ{ao}
ᤐᤥ᤺ᤱᤔᤠ<v>:{B}{oo}{do}{NGK}{S}
ᤐᤥᤅ᤻ᤔᤠ<v>:{B}{oo}ᤅ{km}{S} # Dir/LR
ᤐᤠ᤺ᤵᤔᤠ<v>:{B}{A}{do}{pgt}{R}
ᤐᤠᤐ᤻ᤔᤠ<v>:{B}{A}ᤐ{km}{R} # Dir/LR
ᤔᤠ᤺ᤔᤠ<v>:ᤔᤠ{do}{S}
ᤗ᤺ᤵᤔᤠ<v>:ᤗ᤺{tp}{tm}
ᤗᤐ᤻ᤔᤠ<v>:ᤗ{t}{km}{tm}
ᤗ᤺ᤶᤔᤠ<v>:ᤗ{do}{NM}{Dnull}
ᤀᤢ᤺ᤵᤔᤠ<v>:ᤀᤢ{do}{tp}{tm}
ᤀᤢᤐ᤻ᤔᤠ<v>:ᤀᤢᤐ{km}{tm}
ᤑᤧ᤹ᤔᤠ<v>:ᤑᤧ{dg}{N}

LEXICON Noun-Stems
ᤐᤢ:ᤐᤢ
ᤋᤱ:ᤋᤱ
ᤀᤠᤶᤒᤣ:ᤀᤠᤶᤒᤣ
ᤛᤠᤵᤗᤠ:ᤛᤠᤵᤗᤠ
ᤖᤠ᤺ᤶ:ᤖᤠ᤺{m}
ᤖᤠᤔ᤻:ᤖᤠᤔ᤻ # Dir/LR
ᤏᤠᤰ:ᤏᤠ{k}
ᤔᤣᤴᤇᤠ:ᤔᤣᤴᤇᤠ
ᤗᤠᤶᤛ᤹:ᤗᤠᤶᤛ᤹
ᤏᤠᤵᤔᤡ:ᤏᤠᤵᤔᤡ
ᤂᤩᤠ:ᤂᤩᤠ
ᤀᤕᤰᤐᤠ:ᤀᤕᤰᤐᤠ
ᤁᤖᤁᤘᤠ:ᤁᤖᤁᤘᤠ
ᤔᤠ:ᤔ{A}
ᤐᤠ:ᤐ{A}
ᤐᤡᤋ:ᤐᤡᤋ
ᤀᤡᤗᤠᤶ:ᤀᤡᤗᤠ{m}
ᤆᤠ᤺ᤔᤠᤱ:ᤆᤠ᤺ᤔᤠ{ng}
ᤛᤣᤒᤠ:ᤛᤣᤒᤠ
ᤆᤢᤶ:{ch}{u}{m}
ᤋᤣ᤺ᤱ:ᤋᤣ᤺{ng}
ᤋᤣᤅ᤻:ᤋᤣᤅ᤻ # Dir/LR
ᤏᤣᤐᤠᤸ:ᤏᤣᤐᤠ{ll}
ᤁᤠᤌᤔᤠᤴᤍᤢ:ᤁᤠᤌᤔᤠᤴᤍᤢ
ᤐᤩᤠᤱᤛᤡ:ᤐᤩᤠᤱᤛᤡ
ᤑᤥᤃᤡᤍᤥ:ᤑᤥᤃᤡᤍᤥ
ᤕᤠᤰᤌᤢᤱ:ᤕᤠᤰᤌᤢ{ng}
ᤏᤡᤱᤘᤠ᤹ᤑᤢᤔᤠᤱᤅᤡᤸᤗᤧ:ᤏᤡᤱᤘᤠ᤹ᤑᤢᤔᤠᤱᤅᤡᤸᤗᤧ
ᤕᤠ᤹:ᤕᤠ᤹
ᤆᤱ:ᤆ{ng}
ᤕᤠᤰᤌᤢᤶᤒᤠ:ᤕᤠᤰᤌᤢᤶᤒ{A}
ᤐᤧᤏᤡ:ᤐᤧᤏ{i}
ᤕᤠᤖᤡᤰ:ᤕᤠᤖᤡ{k}
ᤒᤡᤆᤠᤖ:ᤒᤡᤆᤠᤖ
ᤐᤠᤴ:ᤐᤠ{N}
ᤔᤧᤴᤆᤡᤀᤠ:ᤔᤧᤴᤆᤡᤀᤠ
ᤔᤏᤠ:ᤔᤏᤠ
ᤏᤠᤖᤠ:ᤏᤠᤖᤠ
ᤛᤥᤖ:ᤛᤥᤖ
ᤀ᤹ᤍ᤺ᤱ:ᤀ᤹ᤍ᤺{ng}
ᤏᤢ:ᤏᤢ
ᤁᤢᤶᤔᤠ:ᤁᤢᤶᤔ{A}

# numerals
ᤌᤡᤰ:ᤌᤡ{k}
ᤗᤌᤡᤰ:ᤗᤌᤡ{k}
ᤏᤧᤜᤡ:ᤏᤧᤜᤡ
ᤛᤢᤶᤛᤡ:ᤛᤢᤶᤛᤡ
ᤗᤡᤛᤡ:ᤗᤡᤛᤡ
ᤏᤠᤛᤡ:ᤏᤠᤛᤡ
ᤋᤢᤰᤛᤡ:ᤋᤢᤰᤛᤡ
ᤏᤢᤛᤡ:ᤏᤢᤛᤡ
ᤕᤧᤜᤡ:ᤕᤧᤜᤡ
ᤑᤠᤱᤛᤡ:ᤑᤠᤱᤛᤡ
ᤒᤥᤱ:ᤒᤥ{ng}

PATTERN Number
Numeral [<num>:]

LEXICON Numeral
/([0123456789])+/
/([0123456789])+[.,]([0123456789])+/

/([᥆᥇᥈᥉᥊᥋᥌᥍᥎᥏])+/
/([᥆᥇᥈᥉᥊᥋᥌᥍᥎᥏])+[.,]([᥆᥇᥈᥉᥊᥋᥌᥍᥎᥏])+/

LEXICON Punctuation

᥅<sent>:᥅
॥<sent>:॥
᥄<sent>:᥄
।<sent>:।
᥀<sent>:᥀

.<sent>:.
..<sent>:..
...<sent>:...
;<sent>:;
\:<sent>:\:
!<sent>:!
-<guio>:-
—<guio>:—
,<cm>:,
?<sent>:? 
'<apos>:'
"<quot>:"
«<lquot>:«
»<rquot>:»
”<rquot>:”
“<lquot>:“
(<lpar>:(
\]<rpar>:\]
\[<lpar>:\[
)<rpar>:)
\\<sent>:\\
\/<sent>:\/


# Resources:
# https://github.com/apertium/lexd/blob/master/Usage.md
# https://wiki.apertium.org/wiki/List_of_symbols
# https://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
# https://wiki.apertium.org/wiki/Lttoolbox_and_lexc
# https://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
# https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
# https://github.com/hfst/
# https://wiki.apertium.org/wiki/Hfst

