# Morphological Transducer for Limbu

# Part of speech categories
# <n>      # Noun
# <v>      # Verb
# <adj>    # Adjective
# <num>    # Numeral

# <sent>   # Sentence-ending punctuation
# <guio>   # Hyphen
# <cm>     # Comma
# <apos>   # Apostrophe
# <quot>   # Quotation mark
# <lquot>  # Left quotation mark
# <rquot>  # Right quotation mark
# <lpar>   # Left parenthesis
# <rpar>   # Right parenthesis

# Number morphology
# <pl>     # Plural
# <sg>     # Singular
# <du>     # Dual

# Other symbols
# >        # Morpheme boundary

###############################################################################
###                       M O R P H O T A C T I C S                         ###
###############################################################################

PATTERNS
Number
Punctuation
Nouns
Verbs
Adjectives
Determiners-Words
Conjunctions
Adverbs
Question-Particles
CaseMarker
Pronoun-Words
Prepositions [ <pr>: ]
Complementizer
Concession
Interjection
TopicMarker
Evidentiality

PATTERN Interjection
Ij-Stem [ <ij>: ] ( [ +:> ]Question-Particles)?

PATTERN Adverbs
AdverbStem [ <adv>: ] ( [ +:> ]Adverbs2) [ <adv>: ] ( [ +:> ]Complementizer)?
AdverbStem [ <adv>: ] ( [ +:> ]Complementizer)?
AdverbStem [ <adv>: ] Concession
AdverbStem [ <adv>: ] Nominalizer

PATTERN Determiners-Words
(Adverbs [ +:> ] )? Determiners [ <det>: ]

PATTERN Pronoun-Words
Pronouns PossessiveSuff? ( [ +:> ]Adverbs)?
Pronouns Cases?
DemPrn Plurals? Cases?
DefPrn
IntPrn Nominalizer?
IndPrn

PATTERN Nouns
(Adjectives [ +:> ] )? Voice?(1) PossessivePrn?(1) Noun-Stems Plurals Voice?(2) PossessivePrn?(2) Cases?
Noun-Stems Nominalizer Plurals ( [ +:> ] Adjectives)
Noun-Stems Nominalizer Imperious? Cases? ( [ +:> ] Adverbs)
(Adjectives [ +:> ] )? Noun-Stems Nominalizer
Noun-Stems Attr

PATTERN Verbs
( (Adverbs [ +:> ] )? SubjPersonNumber(1) SubjPersonNumber(4) Negation?(1) Verb-Stems Negation?(2) Reflexivity&Port? Tense SubjPersonNumber(5) SubjPersonNumber(2) ObjSlot SubjPersonNumber(6) NegationSuff? SubjPersonNumber(7) ObjNumber SubjPersonNumber(8) SubjPersonNumber(3) NegationSuff2? Mode? Aspect Clitic? Nominalizer? Subord? ( [ +:> ] Adverbs)? )[^[past,nonpast],^[ssg1,ssg23],^[noport,bigport]]
( (Adverbs [ +:> ] )? SubjPersonNumber(1) SubjPersonNumber(4)? Negation?(1) Verb-Stems Negation?(2) Reflexivity&Port? Tense SubjPersonNumber(5)? SubjPersonNumber(2) ObjSlot SubjPersonNumber(6)? NegationSuff? SubjPersonNumber(7)? ObjNumber SubjPersonNumber(8)? SubjPersonNumber(3) NegationSuff2? Mode? Aspect Clitic? Nominalizer? Subord? ( [ +:> ] Adverbs)? )[^[past,nonpast],^[ssg1,ssg23],^[noport,bigport]]  # Dir/LR
Voice?(1) Negation?(1) Verb-Stems GerundMarker Negation?(2) Voice?(2) Cases? Subord? Concession? ( [ +:> ] Adverbs)?
Verb-Stems GerundMarker Imperious ( [ +:> ] Adverbs)?
Voice?(1) Verb-Stems Voice?(2) Nominalizer Cases? Subord|Concession
Verb-Stems GerundMarker ObjNumber|Reflexivity&Port ( [ +:> ] Adverbs)?
Verb-Stems Verb2 Concession

LEXICON PossessiveSuff
<pos>:>{i}{N}

LEXICON PossessivePrn(2)
:ᤀᤠ>               <px1sg>:
:ᤀᤠᤴᤆᤡ>         <px1duincl>:
:ᤀᤠᤴᤆᤡᤅᤠ>       <px1duexcl>:
:ᤀᤠᤏᤡ>          <px1plincl>:
:ᤀᤠᤏᤡᤅᤠ>        <px1plexcl>:
:ᤁᤠ>              <px2sg>:    # Dir/LR
:{K}{E}>         <px2sg>:
:ᤂᤧᤴᤆᤡ>            <px2du>:
:ᤂᤧᤏᤡ>             <px2pl>:
:ᤁᤢ>              <px3sg>:
:ᤂᤢᤴᤆᤡ>            <px3du>:
:ᤂᤢᤴᤆᤡ>            <px3pl>:

LEXICON Voice(2)
:{K}{E}>      <act>:

LEXICON SubjPersonNumber(8)
:ᤀᤠ>[ssg1,noport]           <s_sg1>:[ssg1,noport]           <incl>:[ssg1,noport]            :[ssg1,noport]                :[ssg1,noport]           :>{ng}[ssg1,noport]         :[ssg1,noport]              :>{ng}[ssg1,noport]
:[ssg1,noport]             <s_sg1>:[ssg1,noport]           <incl>:[ssg1,noport]            :[ssg1,noport]                :[ssg1,noport]           :>{ng}[ssg1,noport]         :[ssg1,noport]              :>{ng}[ssg1,noport]
:ᤀᤠ>[ssg1,noport]           <s_sg1>:[ssg1,noport]           <excl>:>ᤃᤧ[ssg1,noport]          :[ssg1,noport]                :[ssg1,noport]           :>{ng}[ssg1,noport]         :[ssg1,noport]              :>{ng}[ssg1,noport]
:ᤀᤠ>[ssg23,noport]          <s_du1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤛ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]                 # Dir/LR
:ᤀᤠ>[ssg23,noport]          <s_du1>:[ssg23,noport]          <excl>:>ᤃᤧ[ssg23,noport]         :ᤔ{E}>[ssg23,noport]          :>ᤛ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]                 # Dir/LR
:ᤀᤠ>[ssg23,noport]          <s_du1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤇ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]
:ᤀᤠ>[ssg23,noport]          <s_du1>:[ssg23,noport]          <excl>:>ᤃᤧ[ssg23,noport]         :ᤔ{E}>[ssg23,noport]          :>ᤇ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]
:[ssg23,noport]            <s_du1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤛ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]                 # Dir/LR
:[ssg23,noport]            <s_du1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤇ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]
:ᤀᤠ>[ssg23,noport]          <s_pl1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:ᤀᤠ>[ssg23,noport]          <s_pl1>:[ssg23,noport]          <excl>:>ᤃᤧ[ssg23,noport]         :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:[ssg23,noport]            <s_pl1>:[ssg23,noport]          <excl>:>ᤃᤧ[ssg23,noport]         :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:[ssg23,bigport]           <s_pl1>:[ssg23,bigport]         <excl>:>[ssg23,bigport]         :ᤔ{E}>[ssg23,bigport]         :[ssg23,bigport]         :[ssg23,bigport]            :>{m}[ssg23,bigport]        :>{m}[ssg23,bigport]             # Dir/LR
:ᤀᤠ>[past,ssg23,noport]     <s_pl1>:[past,ssg23,noport]     <excl>:>ᤃᤧ[past,ssg23,noport]    :ᤔ{E}>[past,ssg23,noport]     :[past,ssg23,noport]     :[past,ssg23,noport]        :>ᤔᤏᤠ[past,ssg23,noport]     :[past,ssg23,noport]
:ᤀᤠ>[past,ssg23,bigport]    <s_pl1>:[past,ssg23,bigport]    <excl>:>[past,ssg23,bigport]    :ᤔ{E}>[past,ssg23,bigport]    :[past,ssg23,bigport]    :[past,ssg23,bigport]       :>ᤔᤏᤠ[past,ssg23,bigport]    :[past,ssg23,bigport]            # Dir/LR
:[ssg23,noport]            <s_pl1>:[ssg23,noport]          <incl>:[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:{K}{E}>[ssg23,noport]     <s_sg2>:[ssg23,noport]                :[ssg23,noport]           :[ssg23,noport]                :[ssg23,noport]         :[ssg23,noport]             :[ssg23,noport]              :[ssg23,noport]
:{K}{E}>[ssg23,noport]     <s_du2>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤛ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]                  # Dir/LR
:{K}{E}>[ssg23,noport]     <s_du2>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤇ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]
:{K}{E}>[ssg23,noport]     <s_pl2>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:[ssg23,noport]            <s_pl2>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]
:[ssg23,noport]            <s_sg3>:[ssg23,noport]                :[ssg23,noport]           :[ssg23,noport]               :[ssg23,noport]          :[ssg23,noport]             :[ssg23,noport]             :[ssg23,noport]
:[ssg23,noport]            <s_du3>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤛ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]                  # Dir/LR
:[ssg23,noport]            <s_du3>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :>ᤇ[ssg23,noport]        :[ssg23,noport]             :>ᤇᤡ[ssg23,noport]           :[ssg23,noport]
:[ssg23,noport]            <s_pl3>:[ssg23,noport]                :[ssg23,noport]           :ᤔ{E}>[ssg23,noport]          :[ssg23,noport]          :[ssg23,noport]             :>{m}[ssg23,noport]         :>{m}[ssg23,noport]

LEXICON Negation(2)
:ᤔᤧ>       <neg>:       # Dir/LR
:ᤔᤧ{N}>    <neg>:
:ᤏ>       <neg>:

LEXICON GerundMarker
<ger>:>ᤔᤢᤏᤠ
<inf>:>ᤔᤧ    # Dir/LR
<inf>:>ᤔᤠ

LEXICON Reflexivity&Port
<refl>:>ᤛᤡ{ng}
<refl>:>ᤏᤧ                # Dir/LR
<o_2>:>ᤏᤧ

LEXICON Tense
<nonpast>:[nonpast]
<past>:>{E}[past]
<past>:>{a}{E}[past]     # Dir/LR

LEXICON ObjSlot
<o_3>:>{a}{u}[noport]
<o_3>:>{u}[noport]       # Dir/LR
<os_pl>:>{a}{I}[noport]
<os_pl>:>{I}[noport]     # Dir/LR
<os_du>:>ᤛᤡ[noport]       # Dir/LR
<os_du>:>ᤇᤡ[noport]
<os_sg>:[noport]

<os_sg12>:>ᤧ[nonpast,noport]
<o_3>:>ᤧ[nonpast,ssg1,noport]
<os_sg12>:>{a}{A}{ng}[past,noport]
<os_sg12>:>{A}{ng}[past,noport]       # Dir/LR
<o_3>:>ᤐᤠ{ng}[past,ssg1,noport]
<​o_3>:>{a}{u}{m}ᤛᤡᤶᤒᤧ[bigport]
<​o_3>:>{u}{m}ᤛᤡᤶᤒᤧ[bigport]            # Dir/LR

LEXICON NegationSuff
<neg>:>{N}

LEXICON Mode
<opt>:>ᤖ
<opt>:>ᤗ   # Dir/LR
<cni>:>ᤔᤧ{N}
<irr>:>ᤃᤏᤡ

LEXICON Aspect
<impf>:>{B}{A}
<perf>:

LEXICON Clitic
<clt><ass>:>ᤖᤥ
<clt><ass>:>ᤗᤥ   # Dir/LR
<clt><depr>:>ᤗᤇ  # Dir/LR
<clt><depr>:>ᤖᤇ

LEXICON ObjNumber
<o_nonsg>:>ᤛᤡ[noport]
<o_nonsg>:[bigport]      # Dir/LR
<o_sg>:[noport]

LEXICON NegationSuff2
<neg>:>{N}
<neg>:>ᤏᤧ{N} # Dir/LR

LEXICON Plurals
<sg>:
<pl>:>ᤄᤠ
<pl>:>ᤜᤠ᤹ # Dir/LR
<du>:>ᤄᤠᤇᤡ
<du>:>ᤜᤠ᤹ᤇᤡ # Dir/LR

LEXICON Cases
<nom>:>ᤒᤧ
<gen>:>{NG}{A}{n}
<gen>:>ᤖᤧ              # Dir/LR
<gen>:>ᤗᤧ              # Dir/LR
<gen>:>{l}ᤗᤧ           # Dir/LR
<gen>:>{ie}{l}ᤗᤧ       # Dir/LR
<gen>:>{a}{ie}{l}ᤗᤧ    # Dir/LR
<voc>:>{aa}{o}
<voc>:>{ana}{e}       # Dir/LR
<voc>:>ᤖᤧ              # Dir/LR
<imp>:>{Ee}
<loc>:>{oo}
<med>:>ᤗᤠᤠ{m}
<ela>:>{oo}ᤏᤢ
<ela>:>{oo}ᤗᤠᤠ{m}       # Dir/LR
<all>:>ᤌᤠᤰ
<all>:>ᤎᤠᤰ             # Dir/LR
<itrt>:>ᤗᤢᤔᤥ
<itrt>:>ᤗᤢᤶᤔᤥ          # Dir/LR
<comp>:>ᤏᤢᤸᤗᤧ
<com>:>ᤏᤢ{ng}
<com>:>ᤏᤢ              # Dir/LR
<abl>:>ᤗᤠᤠ{m}
<abl>:>ᤏᤢ{ng}          # Dir/LR
<erg>:>ᤖᤧ
<erg>:>ᤗᤧ              # Dir/LR
<erg>:>{l}ᤗᤧ           # Dir/LR
<erg>:>{ie}{l}ᤗᤧ       # Dir/LR
<erg>:>{a}{ie}{l}ᤗᤧ    # Dir/LR
<abs>:>{MNG}{Iabs}{N}
<abs>:>{MNG}{EGS}{N}  # Dir/LR
<ins>:>ᤗᤧ
<ins>:>{l}ᤗᤧ           # Dir/LR
<ins>:>{i}{l}ᤗᤧ        # Dir/LR

LEXICON Attr
<attr>:

LEXICON Subord
<sub>:>{ie}{l}ᤗᤧ
<sub>:>{l}ᤗᤧ           # Dir/LR
<sub>:>ᤗᤧ              # Dir/LR

LEXICON Imperious
<def>:>{E}{N}

LEXICON Nominalizer
<nom>:{Bb}{A}

###############################################################################
###                          L E X I C O N                                  ###
###############################################################################

LEXICON Pronouns
ᤀᤠ<prn><pers><p1><sg>:ᤀᤠᤱᤃᤠ{gs}
ᤀᤠ<prn><pers><p1><sg>:ᤅᤠ              # Dir/LR
ᤀᤠ<prn><pers><p1><sg>:ᤀᤡᤱᤃᤠ{gs}         # Dir/LR
ᤀᤠ<prn><pers><p1><du><incl>:ᤀᤠᤴᤇᤡ{gs}
ᤀᤠ<prn><pers><p1><du><excl>:ᤀᤠᤴᤇᤡ>ᤃᤧ
ᤀᤠ<prn><pers><p1><du><excl>:ᤀᤠᤴᤇᤡ>ᤃᤩᤠ    # Dir/LR
ᤀᤠ<prn><pers><p1><du><excl>:ᤀᤠᤴᤇᤡᤅᤠ     # Dir/LR
ᤀᤠ<prn><pers><p1><pl><incl>:ᤀᤠᤏᤡ{gs}
ᤀᤠ<prn><pers><p1><pl><excl>:ᤀᤠᤏᤡ>ᤃᤧ
ᤀᤠ<prn><pers><p1><pl><excl>:ᤀᤠᤏᤡ>ᤃᤣ     # Dir/LR
ᤀᤠ<prn><pers><p1><pl><excl>:ᤀᤠᤏᤡ>ᤃᤩᤠ     # Dir/LR
ᤀᤠ<prn><pers><p1><pl><excl>:ᤀᤠᤏᤡᤅᤠ      # Dir/LR

ᤂᤧᤏᤧ<prn><pers><p2><sg>:ᤂᤧᤏᤧ{gs}
ᤂᤧᤴᤆᤡ<prn><pers><p2><du>:ᤂᤧᤴᤇᤡ
ᤂᤧᤏᤡ<prn><pers><p2><pl>:ᤂᤧᤏᤡ{gs}
ᤂᤡᤏᤡ<prn><pers><p2><pl>:ᤂᤡᤏᤡ{gs}        # Dir/LR
ᤁᤴ<prn><pers><p3><sg>:ᤁᤴ
ᤂᤢᤏᤧ<prn><pers><p3><mf><sg>:ᤂᤢᤏᤧ{gs}
ᤂᤧᤱ<prn><pers><p3><nt><sg>:ᤂᤧᤱ
ᤂᤢᤴᤆᤡ<prn><pers><p3><du>:ᤂᤢᤴᤇᤡ
ᤂᤧᤱᤜᤠ<prn><pers><p3><pl>:ᤂᤧᤱᤜᤠ
ᤂᤢᤏᤡ<prn><pers><p3><pl>:ᤂᤢᤏᤡ{gs}        # Dir/LR
ᤂᤢᤴᤆᤡ<prn><pers><p3><nonsg>:ᤂᤢᤴᤇᤡ{gs}   # Dir/LR
ᤂᤧᤱᤜᤠ<prn><pers><p3><nonsg>:ᤂᤧᤱᤜᤠ        # Dir/LR

LEXICON DemPrn
ᤂᤴ<prn><dem>:ᤂ{N}
ᤂᤧᤴ<prn><dem>:ᤂᤧ{N}
ᤂᤧᤱ<prn><dem>:ᤂᤧ{ng} # Dir/LR
ᤁᤱ<prn><dem>:ᤁ{ng}

LEXICON DefPrn
ᤂᤴᤏᤧᤴ<prn><def>:ᤂᤴᤏ>{E}{N}

LEXICON IntPrn
ᤌᤣ<prn><itg>:ᤌᤣ

LEXICON IndPrn
ᤁᤢᤓᤠ<prn><ind>:ᤁᤢᤓᤠ

LEXICON Prepositions
ᤌᤥ:ᤌᤥ
ᤏᤠ:ᤏᤠ
ᤀᤧᤶᤓᤧᤍᤠᤱᤒᤠ:ᤀᤧᤶᤓᤧᤍᤠᤱᤒᤠ
ᤗᤠᤃᤡ:ᤗᤠᤃᤡ
ᤑᤠ᤹ᤀᤠᤱ:ᤑᤠ᤹ᤀᤠᤱ
ᤌᤠᤖᤡᤰ:ᤌᤠᤖᤡᤰ
ᤋᤃᤡ:ᤋᤃᤡ
ᤗᤧᤵᤔᤠᤱ:ᤗᤧᤵᤔᤠᤱ
ᤁᤢᤛᤡᤱ:ᤁᤢᤛᤡᤱ

LEXICON TopicMarker
ᤆᤀᤡᤲ<top>:ᤆᤀᤡᤲ
ᤁ<top>:ᤁ
ᤛᤧᤴ<top>:ᤛᤧ{N}

LEXICON Complementizer
ᤑᤧᤸᤗᤧ<comp>:{BH}{E}{l}ᤗᤧ

LEXICON Adjectives
ᤏᤢᤒᤠ<adj><m>:ᤏᤢ>ᤒ{A}
ᤏᤢ<adj>:ᤏᤢ
ᤀᤠᤰᤂᤣ<adj>:ᤀᤠᤰᤂ{eg}
ᤀ᤹ᤍ᤺ᤱ<adj>:ᤀ᤹ᤍ᤺{ng}
ᤆᤢᤰᤛᤠ<adj><dim>:ᤆᤢᤰ>ᤛ{A}
ᤂᤶᤍᤠᤱ<adj>:ᤂᤶᤍᤠ{ng}
ᤋᤠᤃᤧᤖᤠ<adj>:ᤋᤠᤃᤧᤖ{A}
ᤘᤣ᤹<adj>:ᤘᤣ{gs}
ᤗᤳᤇᤠ<adj>:ᤗᤳᤇ{A}
ᤕᤶᤒᤠ<adj><m>:ᤕᤶ>ᤒ{A}
ᤕᤖᤡᤰ<adj>:ᤕᤖᤡ{k}
ᤌᤣᤀᤠᤱ<adj>:ᤌᤣᤀᤠ{ng}
ᤜᤠᤱ<adj>:ᤜᤠ{ng}
ᤁᤢᤓᤠ<adj>:ᤁᤢᤓ{A}

LEXICON CaseMarker
ᤋᤠᤵ<nom>:ᤋᤠ{p}
ᤖᤧ<erg>:ᤖᤧ
ᤗᤧ<erg>:ᤗᤧ        # Dir/LR

LEXICON Concession
ᤛᤠᤱ<concess>:ᤛᤠ{ng}

LEXICON Question-Particles
ᤀᤡ<qst>:{a}{I}
ᤀᤡ<qst>:{I}      # Dir/LR

LEXICON Determiners
ᤁᤧᤖᤧᤰ:ᤁᤧᤖᤧ{k}
ᤕᤸᤗᤡᤰ:ᤕᤸᤗᤡ{k}
ᤈᤶᤔ:ᤈᤶᤔ
ᤂ:ᤂ
ᤀᤖᤢ:ᤀᤖᤢ

LEXICON Ij-Stem
ᤛᤣᤘᤠᤖᤥ:ᤛᤣᤘᤠᤖᤥ
ᤔᤧ᤺ᤴ:ᤔᤧ᤺{N}

LEXICON Conjunctions
ᤋᤖ<cnjcoo>:ᤋᤖ
ᤑᤃᤖ<cnjsub>:ᤑᤃᤖ
ᤜᤧᤰᤁᤩᤠᤱ<cnjcoo>:ᤜᤧᤰᤁᤩᤠ{ng}
ᤁᤖ<cnj>:ᤁᤖ
ᤑᤠ᤹ᤀᤠᤱ<cnj>:ᤑᤠ᤹ᤀᤠ{ng}
ᤀᤡᤃᤪ<cnjcoo>:ᤀᤡᤃᤪ

LEXICON Evidentiality
ᤗᤇ<evid>:ᤗᤇ
ᤖᤇ<evid>:ᤖᤇ

LEXICON AdverbStem
ᤂᤶᤓ:ᤂᤶᤓ
ᤂᤶᤓᤧ:ᤂᤶᤓᤧ           # Dir/LR
ᤂᤶᤓᤠ:ᤂᤶᤓᤠ           # Dir/LR
ᤀᤧᤶᤓᤧᤸᤗᤧ:ᤀᤧᤶᤓᤧᤸᤗᤧ
ᤕᤠᤶᤔᤢ:ᤕᤠᤶᤔᤢ
ᤕᤠᤶᤔᤥ:ᤕᤠᤶᤔᤥ          # Dir/LR
ᤖᤳ:ᤖ{t}
ᤀᤠᤱ:{a}{A}{ng}
ᤀᤠᤱ:{A}{ng}         # Dir/LR
ᤀᤠᤓᤣᤍᤠᤱᤒᤠ:ᤀᤠᤓᤣᤍᤠᤱᤒᤠ
ᤀᤠᤓᤣᤍᤠᤱ:ᤀᤠᤓᤣᤍᤠᤱ
ᤀᤠᤸᤗᤠ:ᤀᤠᤸᤗᤠ
ᤔᤧ᤺ᤴᤏ:ᤔᤧ᤺ᤴᤏ
ᤔᤧᤏ᤻ᤏ:ᤔᤧᤏ᤻ᤏ # Dir/LR
ᤌᤧᤕᤠ᤺ᤴᤍᤰ:ᤌᤧᤕᤠ᤺ᤴᤍ{k}
ᤌᤧᤕᤠᤏ᤻ᤍᤰ:ᤌᤧᤕᤠᤏ᤻ᤍ{k}     # Dir/LR
ᤀᤄᤡ:ᤀᤄᤡ
ᤂᤶᤓᤧᤸᤗᤧ:ᤂᤶᤓᤧᤸᤗᤧ
ᤀᤠᤓᤣ:ᤀᤠᤓᤣ
ᤀᤰᤂᤣ:ᤀᤰᤂᤣ
ᤏᤢᤖᤡᤰ:ᤏᤢᤖᤡ{k}
ᤀᤳᤋᤡ:ᤀᤳᤋᤡ
ᤜᤧᤰᤁᤩᤠᤱ:ᤜᤧᤰᤁᤩᤠ{ng}
ᤀᤠᤸᤗ:ᤀᤠᤸᤗ
ᤛᤠ᤹ᤖᤡᤰ:ᤛᤠ{gs}ᤖᤡ{k}
ᤆᤡ:ᤆᤡ
ᤜᤧᤰᤁᤧ:ᤜᤧᤰᤁᤧ
ᤜᤧᤰᤁᤣ:ᤜᤧᤰᤁᤣ  # Dir/LR
ᤌᤣᤀᤠᤱ:ᤌᤣᤀᤠ{ng}
ᤀᤣᤃᤠᤱ:ᤀᤣᤃᤠ{ng}

ALIAS AdverbStem Adverbs2

LEXICON Verb-Stems
ᤀᤡᤶᤔᤠ<v>:ᤀᤡ{P}{S}
ᤗᤡᤶᤔᤠ<v>:ᤗᤡᤶ{D}
ᤛᤥ᤺ᤔᤠ<v>:ᤛᤥ᤺{S}
ᤖᤠ᤺ᤰᤔᤠ<v>:ᤖᤠ᤺ᤰ{s}
ᤖᤠᤁ᤻ᤔᤠ<v>:ᤖᤠᤁ᤻{s}              # Dir/LR
ᤗᤠᤁ᤻ᤔᤠ<v>:ᤗᤠᤁ᤻{s}              # Dir/LR
ᤗᤠ᤺ᤰᤔᤠ<v>:ᤗᤠ᤺ᤰ{s}              # Dir/LR
ᤗᤠ᤺ᤰᤔᤠ<v>:ᤗᤠ᤺ᤰ{S}              # Dir/LR
ᤗᤠᤁ᤻ᤔᤠ<v>:ᤗᤠᤁ᤻{S}              # Dir/LR
ᤐᤣ᤺ᤰᤔᤠ<v>:{B}{e}{do}{K}
ᤐᤣᤁ᤻ᤔᤠ<v>:{B}{e}{K}{km}      # Dir/LR
ᤋᤠ᤺ᤔᤠ<v>:{T}{do}
ᤏᤡᤔᤠ<v>:ᤏᤡ{So}
ᤘᤠᤔᤠ<v>:ᤘᤠ{gs}
ᤛᤧᤶᤔᤠ<v>:ᤛᤧ{do}{R}
ᤆᤥ᤺ᤰᤔᤠ<vbdo>:{ch}{oo}{do}{Kk}
ᤆᤥᤁ᤻ᤔᤠ<vbdo>:{ch}{K}{km}     # Dir/LR
ᤆᤠᤔᤠ<v>:ᤆ{ao}
ᤐᤥ᤺ᤱᤔᤠ<v>:{B}{oo}{do}{NGK}{S}
ᤐᤥᤅ᤻ᤔᤠ<v>:{B}{oo}ᤅ{km}{S}  # Dir/LR
ᤐᤠ᤺ᤵᤔᤠ<v>:{B}{A}{do}{pgt}{R}
ᤐᤠᤐ᤻ᤔᤠ<v>:{B}{A}ᤐ{km}{R}    # Dir/LR
ᤔᤠ᤺ᤔᤠ<v>:ᤔᤠ{do}{S}
ᤗ᤺ᤵᤔᤠ<v>:ᤗ᤺{tp}{tm}
ᤗᤐ᤻ᤔᤠ<v>:ᤗ{t}{km}{tm}
ᤗ᤺ᤶᤔᤠ<v>:ᤗ{do}{NM}{Dnull}
ᤀᤢ᤺ᤵᤔᤠ<v>:ᤀᤢ{do}{tp}{tm}
ᤀᤢᤐ᤻ᤔᤠ<v>:ᤀᤢᤐ{km}{tm}        # Dir/LR
ᤑᤧ᤹ᤔᤠ<v>:ᤑᤧ{dg}{N}
ᤋᤧᤶᤔᤠ<v>:ᤋᤧ{t}
ᤋᤥ᤺ᤔᤠ<v>:ᤋᤥ{do}
ᤏᤠᤘᤠᤔᤠ<v>:ᤏᤠᤘᤠ # unsure
ᤋᤠ᤹ᤔᤠ<v>:ᤋ{R}{do}{ng} # unsure

ALIAS Verb-Stems Verb2

LEXICON Noun-Stems
ᤐᤢ<n>:ᤐᤢ
ᤋᤱ<n>:ᤋᤱ
ᤀᤠᤶᤒᤣ<n>:ᤀᤠᤶᤒᤣ
ᤛᤠᤵᤗᤠ<n>:ᤛᤠᤵᤗᤠ
ᤖᤠᤔ᤻<n>:ᤖᤠᤔ᤻ # Dir/LR
ᤏᤠᤰ<n>:ᤏᤠ{k}
ᤔᤣᤴᤇᤠ<n>:ᤔᤣᤴᤇᤠ
ᤗᤠᤶᤛ᤹<n>:ᤗᤠᤶᤛ᤹
ᤏᤠᤵᤔᤡ<n>:ᤏᤠᤵᤔᤡ
ᤂᤩᤠ<n>:ᤂᤩᤠ
ᤀᤕᤰᤐᤠ<n>:ᤀᤕᤰ>ᤐ{Aabs}
ᤁᤖᤁᤘᤠ<n>:ᤁᤖᤁᤘᤠ
ᤔᤠ<n>:ᤔ{A}
ᤐᤠ<n>:ᤐ{A}
ᤐᤡᤋ<n>:ᤐᤡᤋ
ᤀᤡᤗᤠᤶ<n>:ᤀᤡᤗᤠ{m}
ᤛᤣᤒᤠ<n>:ᤛᤣᤒᤠ
ᤆᤢᤶ<n>:{ch}{u}{m}
ᤋᤣ᤺ᤱ<n>:ᤋᤣ᤺{ng}
ᤋᤣᤅ᤻<n>:ᤋᤣᤅ᤻ # Dir/LR
ᤁᤠᤌᤔᤠᤴᤍᤢ<n>:ᤁᤠᤌᤔᤠᤴᤍᤢ
ᤐᤩᤠᤱᤛᤡ<n>:ᤐᤩᤠᤱᤛᤡ
ᤕᤠᤰᤌᤢᤱ<n>:ᤕᤠᤰᤌᤢ{ng}
ᤕᤠ᤹<n>:ᤕᤠ᤹
ᤆᤱ<n>:ᤆ{ng}
ᤕᤠᤰᤌᤢᤶᤒᤠ<n>:ᤕᤠᤰᤌᤢᤶ>ᤒ{Aabs}
ᤐᤧᤏᤡ<n>:ᤐᤧᤏ{i}
ᤕᤠᤖᤡᤰ<n>:ᤕᤠᤖᤡ{k}
ᤒᤡᤆᤠᤖ<n>:ᤒᤡᤆᤠᤖ
ᤐᤠᤴ<n>:ᤐᤠ{do}{N}
ᤐᤠᤏ᤻<n>:ᤐᤠ{do}{N}  # Dir/LR
ᤔᤧᤴᤆᤡᤀᤠ<n>:ᤔᤧᤴᤆᤡᤀᤠ
ᤔᤏᤠ<n>:ᤔᤏᤠ
ᤏᤠᤖᤠ<n>:ᤏᤠᤖᤠ
ᤛᤥᤖ<n>:ᤛᤥᤖ
ᤀ᤹ᤍ᤺ᤱ<n>:ᤀ᤹ᤍ᤺{ng}
ᤏᤢ<n>:ᤏᤢ
ᤁᤢᤶᤔᤠ<n>:ᤁᤢᤶᤔ{A}
ᤁᤏᤠᤶ<n>:ᤁᤏᤠ{m}
ᤁᤰ<n>:ᤁ{k}
ᤁᤰᤁᤓᤡᤰᤏᤠ<n>:ᤁᤰᤁᤓᤡᤰᤏ{A}
ᤁᤰᤑᤠ<n>:ᤁᤰᤑ{A}
ᤁᤰᤑᤣ᤹<n>:ᤁᤰᤑᤣ᤹
ᤁᤰᤗᤠ<n>:ᤁᤰᤗ{A}
ᤁᤰᤗᤡᤰᤔᤢᤕᤠ<n>:ᤁᤰᤗᤡᤰᤔᤢᤕ{A}
ᤁᤰᤗᤡᤰᤘᤠ<n>:ᤁᤰᤗᤡᤰᤘ{A}
ᤁᤰᤗᤡᤎᤡ᤺ᤶᤒᤠ<n>:ᤁᤰᤗᤡᤎᤡ᤺ᤶᤒ{A}
ᤁᤰᤗᤡᤎᤡᤔ᤻ᤒᤠ<n>:ᤁᤰᤗᤡᤎᤡᤔ᤻ᤒ{A}  # Dir/LR
ᤁᤰᤗᤧᤰᤂᤣ<n>:ᤁᤰᤗᤧᤰᤂᤣ
ᤋᤖ᤺ᤱ<n>:ᤋᤖ᤺{ng}
ᤋᤖᤅ᤻<n>:ᤋᤖ{ng}{km}    # Dir/LR
ᤋᤠᤱᤛᤠᤱ<n>:ᤋᤠᤱᤛᤠ{ng}
ᤀᤡᤰᤛᤠᤍᤡᤱ<n>:ᤀᤡᤰᤛᤠᤍᤡ{ng}
ᤀᤡᤰᤛᤠ<n>:ᤀᤡᤰᤛ{A}
ᤂᤠᤶᤒᤣ᤺ᤰ<n>:ᤂᤠᤶᤒᤣ᤺{k}
ᤆᤀᤡ᤺ᤳ<n>:ᤆᤀᤡ{do}{t}
ᤏᤠᤘᤠ<n>:ᤏᤠᤘ{A}
ᤏᤡᤱᤘᤠ᤹<n>:ᤏᤡᤱᤘᤠ{gs}
ᤕᤠ᤺ᤶᤒᤰ<n>:ᤕᤠ᤺ᤶᤒ{k}
ᤕᤠᤔ᤻ᤒᤰ<n>:ᤕᤠᤔ᤻ᤒ{k}       # Dir/LR
ᤜᤠᤱ<n>:ᤜᤠ{ng}
ᤜᤠᤱᤅᤡᤸᤗᤧ<n>:ᤜᤠᤱᤅᤡᤸᤗᤧ
ᤔᤧᤴᤇᤠᤶ<n>:ᤔᤧᤴᤇᤠ{m}
ᤀᤡᤛᤪᤠᤀᤡᤗᤒᤠ<n>:ᤀᤡᤛᤪᤠᤀᤡᤗ>ᤒ{Aabs}

# names of god
ᤀᤸᤗᤧᤰᤂᤠ<n>:ᤀᤸᤗᤧᤰᤂᤠ
ᤀᤸᤗᤧᤰᤂᤠᤔᤠᤱ<n>:ᤀᤸᤗᤧᤰᤂᤠᤔᤠ{ng}
ᤀᤠᤗᤧᤰᤂᤠᤔᤠᤱ<n>:ᤀᤠᤗᤧᤰᤂᤠᤔᤠ{ng}
ᤋᤥᤗᤡᤱᤛᤥᤔᤢ<n>:ᤋᤥᤗᤡᤱᤛᤥᤔᤢ
ᤏᤡᤱᤘᤠ᤹ᤑᤢᤔᤠ<n>:ᤏᤡᤱᤘᤠ᤹ᤑᤢᤔ{A}
ᤏᤡᤱᤘᤠ᤹ᤑᤢ<n>:ᤏᤡᤱᤘᤠ᤹ᤑᤢ
ᤏᤡᤱᤘᤠ᤹ᤑᤢᤔᤠᤱ<n>:ᤏᤡᤱᤘᤠ᤹ᤑᤢᤔᤠ{ng}
ᤏᤡᤱᤘᤠ᤹ᤑᤢᤔᤠᤱᤅᤡᤸᤗᤧ<n>:ᤏᤡᤱᤘᤠ᤹ᤑᤢᤔᤠᤱᤅᤡᤸᤗᤧ
ᤆᤠ᤺ᤔᤠᤱ<n>:ᤆᤠ᤺ᤔᤠ{ng}
ᤕᤜᤘᤣ<n>:ᤕᤜᤘᤣ

# time Words
ᤐᤡᤴᤆᤧᤵ<n>:ᤐᤡᤴᤆᤧ{p}
ᤐᤣᤔᤢᤰ<n>:ᤐᤣᤔᤢ{k}
ᤁᤢᤔᤢᤰ<n>:ᤁᤢᤔᤢ{k}
ᤕᤧᤴ<n>:ᤕᤧ{N}
ᤕᤧᤴᤃᤠᤵ<n>:ᤕᤧᤴᤃᤠ{p}
ᤗᤠ<n>:ᤗ{A}
ᤋᤱᤒᤣ<n>:ᤋᤱᤒᤣ

# days of the week
ᤋᤧᤏᤧᤒᤠ<n>:ᤋᤧᤏᤧᤒ{A}
ᤑᤥᤖᤠᤒᤠ<n>:ᤑᤥᤖᤠᤒ{A}
ᤔᤡᤰᤜᤠᤱᤒᤠ<n>:ᤔᤡᤰᤜᤠᤱᤒ{A}
ᤂᤣᤰᤛᤢᤒᤠ<n>:ᤂᤣᤰᤛᤢᤒ{A}
ᤁᤢᤖᤢᤵᤐᤠ<n>:ᤁᤢᤖᤢᤵᤐ{A}
ᤑᤛᤢᤵᤐᤠ<n>:ᤑᤛᤢᤵᤐ{A}
ᤛᤧᤴᤇᤪᤧᤱᤒᤠ<n>:ᤛᤧᤴᤇᤪᤧᤱᤒ{A}

# seasons
ᤜᤠᤱᤘᤠ᤺ᤶᤔᤠ<n>:ᤜᤠᤱᤘᤠ᤺ᤶᤔ{A}
ᤋᤱᤘᤠ᤺ᤶᤔᤠ<n>:ᤋᤱᤘᤠ᤺ᤶᤔ{A}
ᤆᤢᤱᤘᤠ᤺ᤶᤔᤠ<n>:ᤆᤢᤱᤘᤠ᤺ᤶᤔ{A}
ᤑᤢᤱᤘᤠ᤺ᤶᤔᤠ<n>:ᤑᤢᤱᤘᤠ᤺ᤶᤔ{A}

# months
ᤁ᤺ᤰᤑᤧᤰᤘᤠ<n>:ᤁ᤺ᤰᤑᤧᤰᤘ{A}
ᤛ᤺ᤵᤑᤧᤰᤘᤠ<n>:ᤛ᤺ᤵᤑᤧᤰᤘ{A}
ᤆᤧᤖᤧᤱᤏᤠᤶ<n>:ᤆᤧᤖᤧᤱᤏᤠ{m}
ᤌᤧᤖᤧᤱᤏᤠᤶ<n>:ᤌᤧᤖᤧᤱᤏᤠ{m}
ᤁᤠᤵᤔᤣ᤺ᤵᤐᤠ<n>:ᤁᤠᤵᤔᤣ᤺ᤵᤐ{A}
ᤌᤰᤔᤣ᤺ᤵᤐᤠ<n>:ᤌᤰᤔᤣ᤺ᤵᤐ{A}
ᤛᤡᤛᤣ᤺ᤰᤐᤠ<n>:ᤛᤡᤛᤣ᤺ᤰᤐ{A}
ᤌᤧᤛᤣ᤺ᤰᤐᤠ<n>:ᤌᤧᤛᤣ᤺ᤰᤐ{A}
ᤛᤡᤛᤣ᤺ᤰᤖᤥ<n>:ᤛᤡᤛᤣ᤺ᤰᤖ{oo}
ᤌᤧᤛᤣ᤺ᤰᤁᤪᤥ<n>:ᤌᤧᤛᤣ᤺ᤰᤁᤪ{oo}
ᤛᤧᤴᤇᤧᤱᤗᤠ<n>:ᤛᤧᤴᤇᤧᤱᤗ{A}
ᤛᤡᤛᤣ᤺ᤰᤗᤠ<n>:ᤛᤡᤛᤣ᤺ᤰᤗ{A}

# numerals
ᤜᤥᤵ<num>:ᤜᤥ{p}
ᤌᤡᤰ<num>:ᤌᤡ{k}
ᤗᤌᤡᤰ<num>:ᤗᤌᤡ{k}    # Dir/LR
ᤏᤧᤜᤡ<num>:ᤏᤧᤜᤡ
ᤛᤢᤶᤛᤡ<num>:ᤛᤢᤶᤛᤡ
ᤗᤡᤛᤡ<num>:ᤗᤡᤛᤡ
ᤏᤠᤛᤡ<num>:ᤏᤠᤛᤡ
ᤋᤢᤰᤛᤡ<num>:ᤋᤢᤰᤛᤡ
ᤏᤢᤛᤡ<num>:ᤏᤢᤛᤡ
ᤕᤧᤜᤡ<num>:ᤕᤧᤜᤡ
ᤑᤠᤱᤛᤡ<num>:ᤑᤠᤱᤛᤡ
ᤒᤥᤱ<num>:ᤒᤥ{ng}
ᤌᤡᤒᤥᤱ<num>:ᤌᤡᤒᤥ{ng}   # Dir/LR
ᤌᤡᤰᤌᤡᤰ<num>:ᤌᤡᤰᤌᤡ{k}
ᤌᤡᤰᤏᤧᤳ<num>:ᤌᤡᤰᤏᤧ{t}
ᤌᤡᤰᤛᤢᤶ<num>:ᤌᤡᤰᤛᤢ{m}
ᤌᤡᤰᤗᤡ<num>:ᤌᤡᤰᤗᤡ
ᤌᤡᤰᤅᤠ<num>:ᤌᤡᤰᤅ{A}
ᤌᤡᤰᤋᤢᤰ<num>:ᤌᤡᤰᤋᤢ{k}
ᤌᤡᤰᤏᤢ<num>:ᤌᤡᤰᤏᤢ
ᤌᤡᤰᤕᤧᤳ<num>:ᤌᤡᤰᤕᤧ{t}
ᤌᤡᤰᤑᤠᤱ<num>:ᤌᤡᤰᤑᤠ{ng}
ᤏᤡᤒᤥ᤺ᤱ<num>:ᤏᤡᤒᤥ᤺{ng}
ᤛᤢᤶᤒᤥ᤺ᤱ<num>:ᤛᤢᤶᤒᤥ᤺{ng}
ᤗᤡᤒᤥ᤺ᤱ<num>:ᤗᤡᤒᤥ᤺{ng}
ᤅᤠᤒᤥ᤺ᤱ<num>:ᤅᤠᤒᤥ᤺{ng}
ᤋᤢᤰᤒᤥ᤺ᤱ<num>:ᤋᤢᤰᤒᤥ᤺{ng}
ᤏᤢᤒᤥ᤺ᤱ<num>:ᤏᤢᤒᤥ᤺{ng}
ᤕᤧᤳᤒᤥ᤺ᤱ<num>:ᤕᤧᤳᤒᤥ᤺{ng}
ᤑᤠᤱᤒᤥ᤺ᤱ<num>:ᤑᤠᤱᤒᤥ᤺{ng}
ᤌᤡᤰᤁᤡᤵ<num>:ᤌᤡᤰᤁᤡ{p}

# proper Nouns
ᤖᤠ᤺ᤶ<np>:ᤖᤠ᤺{m}
ᤏᤣᤐᤠᤸ<np>:ᤏᤣᤐᤠ{ll}
ᤑᤥᤃᤡᤍᤥ<np>:ᤑᤥᤃᤡᤍᤥ
ᤁᤖᤍ<np>:ᤁᤖᤍ
ᤌᤧᤈᤱᤒᤥ<np>:ᤌᤧᤈᤱᤒ{oo}
ᤌᤱᤗᤱ<np>:ᤌᤱᤗ{ng}
ᤗᤡᤱᤎᤧᤵ<np>:ᤗᤡᤱᤎᤧ{p}
ᤂᤰᤗᤡᤱ<np>:ᤂᤰᤗᤡ{ng}

PATTERN Number
Numeral [<num>:]

LEXICON Numeral
/([0123456789])+/
/([0123456789])+[.,]([0123456789])+/

/([᥆᥇᥈᥉᥊᥋᥌᥍᥎᥏])+/
/([᥆᥇᥈᥉᥊᥋᥌᥍᥎᥏])+[.,]([᥆᥇᥈᥉᥊᥋᥌᥍᥎᥏])+/

LEXICON Punctuation

᥅<sent>:᥅
॥<sent>:॥
᥄<sent>:᥄
।<sent>:।
᥀<sent>:᥀

.<sent>:.
..<sent>:..
...<sent>:...
;<sent>:;
\:<sent>:\:
!<sent>:!
-<guio>:-
—<guio>:—
,<cm>:,
?<sent>:? 
'<apos>:'
"<quot>:"
«<lquot>:«
»<rquot>:»
”<rquot>:”
“<lquot>:“
(<lpar>:(
\]<rpar>:\]
\[<lpar>:\[
)<rpar>:)
\\<sent>:\\
\/<sent>:\/


# Resources:
# https://github.com/apertium/lexd/blob/master/Usage.md
# https://wiki.apertium.org/wiki/List_of_symbols
# https://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
# https://wiki.apertium.org/wiki/Lttoolbox_and_lexc
# https://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
# https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
# https://github.com/hfst/
# https://wiki.apertium.org/wiki/Hfst

